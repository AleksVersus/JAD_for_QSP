# get.bag
! локация находит для предмета наиболее подходящую сумку
$args[0] = $args[0]	&	!	ай-ди предмета
if $args[1]='': $args[1]='<bag>'	&	!	регулярное выражение, например класс объекта. по умолчанию производится поиск среди сумок
$args['obj'] = $func('get.obj.id',$args[0])	&	!	получаем тело предмета
args['pit']=arrpos('$id_array',$args[0])	&	!	получаем позицию предмета в базе
$args['short_name']=$func('get.tag.cont',$args['obj'])	&	!	короткое имя предмета, ай-ди вида.
! -------------- алгоритм поиска подходящей сумки ------------------------------
	$args['res.021213']='INVENTORY'	&	!	по умолчанию все предметы помещаются в инвентарь, если не будет найдено иного подходящего контейнера.
	! ------------------ поиск сумки без целевого слова ------------------------
		args['arr']=-1
		:bag_non_target
		args['arr']=arrcomp(args['arr']+1,'$object_array','[\s\S]*'+$args[1]+'[\s\S]*')
		! Вычисляем первую попавшуюся сумку
		if no args['arr']<0:
			if $strfind($object_array[args['arr']],'\[:[^:]*]')!'':
			! Если содержит целевое слово, повторяем поиск
				jump 'bag_non_target'
			else
			! Если не содержит целевого слова - сумка подходящая
				$args['res.021213']=$id_array[args['arr']]
			end
		end
	! ------------------ поиск сумки без целевого слова ------------------------
	! ------------------ поиск сумки с подходящим целевым словом ---------------
		! Получаем список целевых слов
		$args['target_list']=$replace($replace($func('get.tag.cont',$args['obj'],'np'),'[','|'),']')
		$args['target_list']=$mid(TRIM($args['target_list']),2)+'| '
		args['tl']=1
		:bag_w_target
		$args['boob']=TRIM($func('get.word.inPos',$args['target_list'],args['tl']))
		! $args['print']+=$args['boob']+' <<args["tl"]>><br>'
		if $args['boob']!'':
			args['arr']=arrcomp('$object_array','[\s\S]*'+$args[1]+'[\s\S]*\[:'+$args['boob']+'\][\s\S]*|[\s\S]*\[:'+$args['boob']+'\][\s\S]*'+$args[1]+'[\s\S]*')
			if args['arr']=-1:
				args['tl']+=1
				jump 'bag_w_target'
			else
				$args['res.021213']=$id_array[args['arr']]
			end
		end
	! ------------------ поиск сумки с подходящим целевым словом ---------------
	! Как видно из последовательности действий, если небудет найдена искомая сумка, предмет будет добавлен в сумку выше по иерархии
! -------------- алгоритм поиска подходящей сумки ------------------------------
$result=$args['res.021213']
--- get.bag ---------------------------------