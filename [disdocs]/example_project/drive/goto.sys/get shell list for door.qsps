# get.shell.inOpen
! Подпрограмма перебирает все предметы на наличие тегов [снаряд] и [дверь]
! Если теги есть, данные из [uron::uron] предмета и $args[0], отправляются программе get.similar.Uron,
! Которая сравнивает каждый тип урона с каждым типом урона и выстраивает список совпадающих типов урона.
! Если возвращённый результат не равен пустому значению, ай-ди снаряда записывается в $shell_iag
! $args[0] - список уронов
$args[0] = $args[0]
args['pos']=-1
:for
args['pos']=arrcomp(args['pos']+1,'$object_array','[\s\S]*\[снаряд метательный\][\s\S]*\[:дверь\][\s\S]*|[\s\S]*\[:дверь\][\s\S]*\[снаряд метательный\][\s\S]*')	&	! находим в массиве позицию элемента содержащего сочетание тегов
if no args['pos']<0:
	if $func('prv.str.inObj',$position_array[args['pos']],'<bag>|<inventory>')='false':	jump 'for'	&	! если предмет не лежит ни в сумке ни в инвентаре, ищем новый
	if $args[0]='' or ($args[0]!'' and $func('get.similar.Uron',$args[0],$func('get.tag.cont',$object_array[args['pos']],'uron'))!''):
	! если урон можно нанести любыми видами атак или конкретным и он присутствует в списке оружия
		$shell_iag[arrsize('$shell_iag')]=$id_array[args['pos']]
	end
	jump 'for'
else
	exit
end
--- get.shell.inOpen ---------------------------------