# [м:0]_общий_вид_локации_места
! Данная локация не является действующей. Это общий вид, указывающий жёсткую конструкцию, используемую при составлении локаций места.
$args[0] = $args[0]	&	!	обязательная строка. Первый аргумент, который должен содержать ай-ди локации. На локациях места не используется
$args[1] = $args[1]	&	!	обязательная строка. Второй аргумент, который содержит управляющие слова или конструкции. При простом посещении "комнаты" он пустой.
$args[2] = $args[2]	&	!	необязательная строка. Аргумент возврата. Если есть, нужно отправить следующие две строки, в противном случае они тоже не нужны.

if $args[1]='':
! обязательная конструкция, позволяющая выполнять включённый в неё скрипт при простом посещении "комнаты"
	if здесь_был[$curloc]=0:
	! необязательная, но популярная конструкция, позволяющая выполнить скрипт однократно при первом посещении "комнаты"
		gosub 'add.new.obj','<заголовок> [name:Дом, который построил Джек:name] [type:здание:type]','',$curloc	&	!	"необязательная, 
		но очень важная строка. Если вы не создадите заголовок для своей локации, на экран будет выводиться надпись НЕИЗВЕСТНАЯ ТЕРРИТОРИЯ,
		что естественно будет сбивать с толку игрока. К тому же заголовок по сути и является объектом локация, позволяет управлять ею и изменять 
		некоторые её свойства"
	end
	!здесь могут быть любые скрипты.
	$lvar['локация.исходник']={В значение этой переменной вносится динамический или статический текст локации, который будет выводиться при простом посещении комнаты.
	Переменная может находиться в любом из мест внутри конструкции
	
	if $args[1]='':
		
	end
	
	Например это может выглядеть так
	if $args[1]='':
		if здесь_был[$curloc]=0:
			$lvar['локация.исходник']='Текст, который вы увидите при первом посещении локации.'
		else
			$lvar['локация.исходник']='Текст, который вы увидите при последующих посещениях локации.'
		end
		$lvar['локация.исходник']='Текст, который вы увидите независимо от того, который раз посещаете локацию.'
	end
	}
	act '<font color=#884488>Действие, которое будет видно только при $GAME_INTERFACE["show.acts"]="show". Безусловный переход.</font>':
		goto '[м:1]_дом'
	end
end
--- [м:0]_общий_вид_локации_места ---------------------------------

# [м:1]_дом
*PL '------- базовое описание --------'
*PL 'Это описание локации из поля "будет выведено в любом случае". Здесь так же можешь использовать динамический текст.'
*PL '(Базовый описаний//Базовая описания)'
*PL '-------- базовое описание -----------'
! типичный вид локации места:
$args[0] = $args[0]	&	!	первый аргумент передаёт ай-ди локации. На локациях места не используется.
$args[1] = $args[1]	&	!	второй аргумент передаёт управляющее слово. Например, если локацией является запертая комната, в которой находится персонаж, войти в эту комнату можно только с разрешения персонажа.
if $args[1]='':
! скрипты выполняемые при посещении локации. 
	if здесь_был[$curloc]=0:
		! -------------- Скрипты выполняемые при первом посещении --------------------------
		! Новый объект ключ
		gosub 'add.new.obj','<key> [name:Ключ от дома, который построил Джек:name] [key:ключ_от_дома:key]',$func('add.new.obj','<keys> [name:Связка ключей:name] [color:0088aa] [oneself] [:keys:]','INVENTORY','KEYS'),'KEY',1
		! Заголовок здесь - является новым объектом игры! Добавляется объект с ай-ди-шником повторяющим название локации
		gosub 'add.new.obj','<заголовок> [name:Дом, который построил Джек:name] [type:здание:type] [place::place] [target:[оружие]:target] [menergy:u1:свет:u1 p1:2000 u2:тьма:u2 p2:155:menergy]','',$curloc
		! Переход на улицу в ссылке
		gosub 'add.new.obj','<link> [name:Выйти на улицу.:name] <goto:[loc:[м:2]_улица:loc] [time:[minute:340]:time]:goto>','','из дома на улицу'
		gosub 'add.new.obj','<link> <goto:[loc:[chest]:loc] [did:сундук;1_гардеробная:did] :goto>','','гардероб в доме'
		gosub 'add.new.obj','<link> [name:Зайти в кузницу.:name] <goto:[loc:[chest]:loc] [did:сундук;1010_простая_кузница:did] [time:[minute:5]:time] :goto>','','кузница в доме'
		gosub 'add.new.obj','<link> [name:Алтарь у печки.:name] <goto:[loc:[chest]:loc] [did:сундук;1020_алтарь:did] [time:[minute:5]:time] [spec:set_altar:spec]:goto>','','алтарь в доме'
		gosub 'add.new.obj','<goto> <goto:[name:<font color=#888800>[говорить с торговцем]</font>:name] [loc:[chest]:loc] [did:торговец;татиан:did]:goto>',$curloc,'GOTO'
		gosub 'add.new.obj','<goto> <goto:[name:Обычная кухня:name] [loc:место;стандартная_кухня:loc] [did:место;дом_который_построил_джек_стандартная кухня:did] [rule:{name:в доме, который построил Джек:name}:rule]:goto>',$curloc,'GOTO'
		gosub 'base.new.obj','[:no_count:] [name:Изумруды:name] [color:008844] [num:изумруд|изумруда|изумрудов:num] ','',$curloc,99
		gosub 'base.new.obj','[:?:] [name:<font color=#008888>Пустой предмет</font>:name] [number] [color:008844] [text:Описание пустого предмета:text]','',$curloc,1
		gosub 'base.new.obj','[:посох_файербол:]'
		gosub 'base.new.obj','[:посох_айсбол:]'
		gosub 'base.new.obj','[:посох_танцующие_молнии:]','','',1,30000
		! $args['f_b_id']=$func('base.new.obj','[:дублоны:]',"","SPACE",2)
		! gs 'add.obj.nocount',$args['f_b_id']
		$args['objs']=$func('add.new.obj','<obj>')
		args['moment1']=MSECSCOUNT
		:fore50
		if args['i']<50000:
			args['t']=rand(0,1000)
			if args['t']=535:
				if args['a']=0: gosub 'base.new.obj','[:ружьё_самопальное:]'
				if args['a']=1: gosub 'base.new.obj','[:ружьё_револьвер:]'
				if args['a']=2: gosub 'base.new.obj','[:патроны_револьверные:]'
				if args['a']=3: gosub 'base.new.obj','[:патроны_револьверные_патронтаж_2:]'
				if args['a']=4: gosub 'base.new.obj','[:патроны_револьверные_патронтаж_6:]'
				if args['a']=5: gosub 'base.new.obj','[:патроны_самопальные:]'
				if args['a']=6: gosub 'base.new.obj','[:радужный_меч:]'
			else
				if args['a']=0: gosub 'base.new.obj','[:ружьё_самопальное:]','',$args['objs']
				if args['a']=1: gosub 'base.new.obj','[:ружьё_револьвер:]','',$args['objs']
				if args['a']=2: gosub 'base.new.obj','[:патроны_револьверные:]','',$args['objs']
				if args['a']=3: gosub 'base.new.obj','[:патроны_револьверные_патронтаж_2:]','',$args['objs']
				if args['a']=4: gosub 'base.new.obj','[:патроны_револьверные_патронтаж_6:]','',$args['objs']
				if args['a']=5: gosub 'base.new.obj','[:патроны_самопальные:]','',$args['objs']
				if args['a']=6: gosub 'base.new.obj','[:радужный_меч:]','',$args['objs']
			end
			args['a']+=1
			if args['a']=7: args['a']=0
			args['i']+=1
			if args['i'] mod 1000 = 0:
				args['d']+=1
				if len(str(args['d']))<2:
					$args['strd']='00'+str(args['d'])
				elseif len(str(args['d']))<3:
					$args['strd']='0'+str(args['d'])
				else
					$args['strd']=str(args['d'])
				end
				*p $args['strd']+'|'
				if args['i'] mod 25000 = 0: *pl
			end
			jump 'fore50'
		end
		args['moment2']=MSECSCOUNT
		$lvar['локация.исходник']+='<br>Время затраченное на генерацию объектов: '+str(args['moment2']-args['moment1'])+'<br>'
		gs 'int.inventory'
		$lvar['локация.исходник']+='текст, который ты увидишь при первом посещении локации<br> Внимание! Этот текст будет виден лишь до тех пор, пока игрок не выполняет никаких действий.<br><br>'
	else
		$lvar['локация.исходник']+='текст, который ты увидишь при поселдующих посещениях локации<br><br><br>'
	end
	! прочие скрипты
	$lvar['локация.исходник']+={[th]Стояло прекрасное [ty]летнее|осеннее|зимнее|весеннее[/ty] утро.|Стоял замечательный [ty]летний|осенний|зимний|весенний[/ty] день.|Стоял необычайно тихий [ty]летний|осенний|зимний|весенний[/ty] вечер.|Тиха [ty]летняя|осенняя|зимняя|весенняя[/ty] украинская ночь.[/th]
Внешней границы пустые обещания. (Дорогой//Дорогая), ты помнишь, о чём мы говорили? (Мальчик Мой//Девочка Моя), кто шёлковыми пальцами расчёсывает твои золотые кудри?
[goto]Вот гардероб в доме, который построил Джек.|[chest]|сундук;1_гардеробная||[м:1]_дом[/goto]
[lnk]кузница в доме|Кузница в доме, который построил Джек.[/lnk]<b>[дорогой//дорогая]</b>
[lnk]алтарь в доме|Жертвенный алтарь в доме, который построил Джек[/lnk]
<DIN5: if a=0: $result="<DIN1:$result="Проверка вложенности DIN удалась":1DIN>" :5DIN>
	[lnk]из дома на улицу|Выйти на улицу из дома, который построил Джек[/lnk]}
	$avar['действие']={!" <act:[name:<font color=#884488>Действие, которое будет воспроизводиться в любом случае</font>:name]:act>"
	goto '[м:0]_общий_вид_локации_места'
	}
	act '<font color=#884488>Действие, которое будет видно только при $GAME_INTERFACE["show.acts"]="show"</font>':
	goto '[м:0]_общий_вид_локации_места'
	end
end
--- [м:1]_дом ---------------------------------

# [м:2]_улица
! типичный вид локации места:
if здесь_был[$curloc]=0:
	! Заголовок здесь - является новым объектом игры! Добавляется объект с ай-ди-шником повторяющим название локации
	gosub 'add.new.obj','<заголовок> [name:Улица, где стоит дом, который построил Джек:name] [type:улица:type]','',$curloc
	! Переходы
	gosub 'add.new.obj','<link> [name:Войти в дом.:name] <goto:[loc:[м:1]_дом:loc] [time:[day:8] [minute:47]:time] [key:ключ_от_дома:key]:goto>','','с улицы в дом'
	! Яблоко
	gosub 'add.new.obj','<link> [name::name] [other:Посреди улицы стоит одинокая берёза.<br>:other] <obj:[io1:[:титановый_лук:] [kolvo:result=rand(2,5):kolvo]:io1]  [io2:[:лыковые_туфли:] [kolvo:result=1:kolvo]:io2]:obj> [respawn:[day:1]:respawn]','',"берёза"
end
$lvar['локация.исходник']={[lnk]с улицы в дом|Войти в дом, который построил Джек[/lnk]
[lnk]берёза|Посреди улицы стоит одинокая берёза.<br>|На берёзе созрело несколько титановых луков.<br>[/lnk]
[goto]Зайти в дом медленно (1 час 23 минуты)|[м:1]_дом| |[hour:1] [minute:23][/goto]}
--- [м:2]_улица ---------------------------------

# место;стандартная_кухня
! --------------------------------------- Обязательная конструкция -------------------------------------------

$args[0]=$args[0]	&	!	передаёт ай-ди локации
$args[1]=$args[1]	&	!	передаёт управляющую конструкцию
$args[2]=$args[2]	&	!	передаёт ай-ди исходной локации

$cvar['chest']=$args[0]
if $cvar[$cvar['chest']]='':
! $cvar удаляется только когда закрывается первый открытый сундук
	$cvar[$cvar['chest']]	=	$args[0]
	$cvar['back'+$cvar['chest']]	=	$args[2]
	$cvar['loc_id'+$cvar['chest']]	=	$args[0]
	$cvar['manage'+$cvar['chest']]	=	$args[1]
end
! ---------------------------------------- Обязательная конструкция -------------------------------------------
if ($args[1]='' or $strfind($args[1],'')!'') or $strfind($args[1],'get.exposition')!'':
	$args['локация.исходник.описание']={}
end
--- место;стандартная_кухня ---------------------------------
