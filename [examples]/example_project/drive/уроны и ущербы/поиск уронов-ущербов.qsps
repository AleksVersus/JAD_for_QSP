# get.uron.list
! функция формирует текст для вывода на экран по имеющимся уронам. VISUALISATION
$args[0] = $args[0]	&	!	ключевое слово
$args[1] = $args[1] 	&	!	список уронов
if $args[2] = '': $args[2]='ff0000' 	&	!	цвет, по умолчанию ярко-красный
args['i']=1
$args['res.021213']='<table align=left valign=top border=0 cellpadding=0 cellspacing=0 width=60%>'
:for_uron
$args['uron_i']=$func('get.tag.cont',$args[1],'u'+$str(args['i']))
args['power_i']=func('get.tag.num',$args[1],'p'+$str(args['i']))
if $args['uron_i']!'':
	if args['power_i']!0: $args['res.021213']+='<tr><td align=left><b>'+$func('base.word.uron',$args[0],$args['uron_i'])+':'+'</b></td><td align=left width=10%><font color=#'+$args[2]+'>'+$func('#indiv#',args['power_i'],100,10)+'</font></td></tr>'
	$args[1]=$TRIM($replace($args[1],'u'+$str(args['i'])+':'+$args['uron_i']+':'+'u'+$str(args['i'])))
	$args[1]=$TRIM($replace($args[1],'p'+$str(args['i'])+':'+$args['uron_i']))
end
args['i']+=1
if len($args[1])>0 and args['i']<100: jump 'for_uron'
$args['res.021213']+='</table>'
$result=$args['res.021213']
--- get.uron.list ---------------------------------

# get.similar.Uron
! Локация сравнивает два набора характеристик
! ВАЖНО! Характеристики должны заключаться в теги u1: :u1 и т.д.
! $args[0] - первый набор
! $args[1] - второй набор
! В результате функция возвращает набор совпадающих характеристик
$args[0] = $args[0]
$args[1] = $args[1]
u_gsu=0
:while2
dynamic "
i_gsu=1
:while1
if $strfind($args[<<u_gsu>>],'u[\d]+:[\s\S]+:u[\d]+')!'':
	$gsu<<u_gsu>>[i_gsu]=$func('get.tag.cont',$args[<<u_gsu>>],'u'+str(i_gsu))
	$args[<<u_gsu>>]=$replace($args[<<u_gsu>>],'u'+str(i_gsu)+':'+$gsu<<u_gsu>>[i_gsu]+':u'+str(i_gsu))
	i_gsu+=1
	jump 'while1'
end",$args[0],$args[1]
u_gsu+=1
if u_gsu<2: jump 'while2'
i_gsu=0
u_gsu=1
:while3
if i_gsu<arrsize('$gsu0'):
	if $gsu0[i_gsu]!''and arrpos('$gsu1',$gsu0[i_gsu])!-1:
		$args['res.021213']+='u<<u_gsu>>:'+$gsu0[i_gsu]+':u<<u_gsu>> '
		u_gsu+=1
	end
	i_gsu+=1
	jump 'while3'
end
$result=$args['res.021213']
killvar '$gsu0'
killvar '$gsu1'
killvar 'i_gsu'
killvar 'u_gsu'
--- get.similar.Uron ---------------------------------

попробуем и сюда вставить какой-то комментарий



# sim.uron.onFull
! Локация сравнивает два набора характеристик
$args[0] = $TRIM($args[0])	&	!	первый набор - основной
$args[1] = $TRIM($args[1])	&	!	второй набор - проверочный
! Результат является верным, когда все характеристики из второго набора по параметрам превышают или равны характеристикам из первого
! наборы приходят в виде u1:djdj:u1 p1:1000 u2:mmd:u2 p2:100 и т.д.
args['i']=1
:надежда_твой_компас_земной
if $strfind($args[0],"u[\d]+:[\s\S]+:u[\d]+")!'':
! проверяем наличие в строке интересующей конструкции
	$args['uron.1']=$func('get.tag.cont',$args[0],"u<<args['i']>>")
	! вычисляем характеристику по текущему номеру
	if $args['uron.1']!"":
	! если характеристика присутствует в строке
		$args['uron.2']=$strfind($args[1],"u[\d]+:"+$args['uron.1']+":u[\d]+")
		!	получаем ту же характеристику из второй строки
		args['uron.2.p']=val($mid($args['uron.2'],2,instr($args['uron.2'],':')-2))
		! получаем номер характеристики из второй строки
		args['uron.2.p']=func('get.tag.num',$args[1],"p<<args['uron.2.p']>>")
		! получаем силу характеристики из второй строки, из первой:
		args['uron.1.p']=func('get.tag.num',$args[0],"p<<args['i']>>")
		if $args['uron.2']='' or args['uron.1.p']>args['uron.2.p']:
		! если характеристика с таким же значением отсутствует в строке, или сила характеристики из первой строки превышает силу характеристики из второй строки
			$result='false'
			! результат - ложь
			exit
		end
		! удаляем характеристику из первой строки
		$args[0] = $replace($args[0],"u<<args['i']>>:"+$args['uron.1']+":u<<args['i']>>")
		$args[0] = $replace($args[0],"p<<args['i']>>:"+$str(args['uron.1.p']))
		$args[0]=$TRIM($args[0])
	end
	args['i']+=1
	jump 'надежда_твой_компас_земной'
end
$result='true'
--- sim.uron.onFull ---------------------------------