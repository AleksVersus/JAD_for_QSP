# int.counter
! локация воспроизводит визуально счётчик по ай-ди.
$args[0] = $args[0]
args['pit']=arrpos('$id_array',$args[0])
$args['obj']=$object_array[args['pit']]
$args['np']=$func('get.tag.cont',$args['obj'],'np')
$args['name']=$func('get.tag.cont',$args['obj'],'name')
$args['color']=$func('get.tag.num',$args['obj'],'color')
$args['short_name']=$func('get.tag.cont',$args['obj'])
if $args['color']='': $args['color']='008888'
if instr($args['np'],'[level]')!0:
	$args['res.021213']+='<table cellpadding=0 cellspacing=0 border=0>'
	$args['res.021213']+='<tr><td><b><font color=#'+$args['color']+' size=5>'+$args['name']+': </font></b></td>'
	$args['l']=$func('get.tag.cont',$args['obj'],'l')
	if $args['l']='': $args['l']='уровень|уровня|уровню|уровень|уровнем|уровне'
	$args['res.021213']+='<td align=right><font color=#'+$func('#+col#',$args['color'],'-22')+'>'+str(point_count[$args['short_name']+'.level'])+" "+$func('get.word.padez',$args['l'],'И')+'</font></td></tr>'
	$args['p']=$func('get.tag.cont',$args['obj'],'p')
	if $args['p']='': $args['p']='Набрано: '
	$args['res.021213']+='<tr><td><font color=#'+$func('#+col#',$args['color'],'-22')+'><b>'+$args['p']+'</b></font></td><td align=right>'+str(point_count[$args['short_name']+'.point'])
	$args['m']=$func('get.tag.cont',$args['obj'],'m')
	if $args['m']='': $args['m']=' из '
	$args['res.021213']+='<b>'+$args['m']+str(point_count[$args['short_name']+'.marker'])+'</b>'
	$args['max']=$func('get.tag.cont',$args['obj'],'max')
	if $args['max']!'':
		$args['res.021213']+='<b>'+$args['max']+str(point_count[$args['short_name']+'.maximum'])+'</b>'
	end
	$args['up']=$func('get.tag.cont',$args['obj'],'up')
	if $args['up']!'':
		$args['res.021213']+='<b>'+$args['up']+str(point_count[$args['short_name']+'.up'])+'.</b>'
	end
	$args['res.021213']+='</td></tr>'
	$args['s']=$func('get.tag.cont',$args['obj'],'s')
	if $args['s']!'':
		$args['res.021213']+='<tr><td colspan=2><br><font color=#'+$func('#+col#',$args['color'],'-44')+'>'+$args['s']+': </font>'+str(point_count[$args['short_name']+'.marker']-point_count[$args['short_name']+'.point'])+'</td></tr>'
	end
	$args['res.021213']+='</table>'
elseif instr($args['np'],'[easy]')!0:
	$args['res.021213']='<font color=#'+$args['color']+'><b>'+$args['name']+': </b></font>'+str(point_count[$args['short_name']+'.point'])
	$args['max']=$func('get.tag.cont',$args['obj'],'max')
	if $args['max']!'':
		$args['res.021213']+='<b>'+$args['max']+str(point_count[$args['short_name']+'.maximum'])+'</b>'
	end
	$args['up']=$func('get.tag.cont',$args['obj'],'up')
	if $args['up']!'':
		$args['res.021213']+='<b>'+$args['up']+str(point_count[$args['short_name']+'.up'])+'.</b>'
	end
end
$result=$args['res.021213']
--- int.counter ---------------------------------