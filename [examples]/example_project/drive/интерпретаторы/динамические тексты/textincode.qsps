QSP-Game Интерпретатор получает текст, преобразует текст в код и выдаёт на гора результат.
! Если в интерпретатор попадает неизвестная конструкция, интерпретатор херит текст к чертям
$[0]	&	!	-	динамический текст
$[1]	&	!	-	дополнительные данные
# int.DIN
local $dyn_txt_, $mode_ = $args[0], $args[1]
if $strfind($dyn_txt_,'\[th\][\s\S]*?\[\/th\]') <> '' and instr($dyn_txt_,'[th]')=1:
	$result = @int.time.hours(@em.tag.getCont($dyn_txt_,'th'))
elseif $strfind($dyn_txt_,'\[ty\][\s\S]*?\[\/ty\]') <> '' and instr($dyn_txt_,'[ty]')=1:
	$result = @int.time.years(@em.tag.getCont($dyn_txt_,'ty'))
elseif $strfind($dyn_txt_,'\[tm\][\s\S]*\[\/tm\]') <> '' and instr($dyn_txt_,'[tm]')=1:
	$result = @int.time.month(@em.tag.getCont($dyn_txt_,'tm'))
elseif $strfind($dyn_txt_,'\[td\][\s\S]*\[\/td\]') <> '' and instr($dyn_txt_,'[td]')=1:
	$result = @int.time.days(@em.tag.getCont($dyn_txt_,'td'))
elseif $strfind($dyn_txt_,'\[chestname\][\s\S]+\[\/chestname\]') <> '' and instr($dyn_txt_,'[chestname]')=1:
	$result = @int.chest.name(@em.tag.getCont($dyn_txt_,'chestname'))
elseif $strfind($dyn_txt_,'\[var\][\s\S]+\[\/var\]') <> '' and instr($dyn_txt_,'[var]')=1:
	$result = @int.var(@em.tag.getCont($dyn_txt_,'var'))
elseif $strfind($dyn_txt_,'\[lnk\][\s\S]+\[\/lnk\]') <> '' and instr($dyn_txt_,'[lnk]')=1:
	$result = @int.link($TRIM(@em.tag.getCont($dyn_txt_,'lnk')))
elseif $strfind($dyn_txt_,'\[goto\][\s\S]+\[\/goto\]') <> '' and instr($dyn_txt_,'[goto]')=1:
	$result = @int.dyn.goto($TRIM(@em.tag.getCont($dyn_txt_,'goto')))
elseif $strfind($dyn_txt_,'\[back\][\s\S]+\[\/back\]') <> '' and instr($dyn_txt_,'[back]')=1:
	$result = @int.dyn.back($TRIM(@em.tag.getCont($dyn_txt_,'back')))
elseif $strfind($dyn_txt_,'\[[\s\S]*?\/\/[\s\S]*?\]') <> '' or $strfind($dyn_txt_,'\([\s\S]*?\/\/[\s\S]*?\)') <> '':
	$result = @int.word.SEX($dyn_txt_)
elseif $strfind($dyn_txt_,'<DIN(\d+):[\s\S]*:\1DIN>') <> 0:
	local temp_ = val($strfind($strfind($dyn_txt_,'<DIN\d+:'),'\d+'))
	$dyn_txt_ = $replace($replace($dyn_txt_,"<DIN<<temp_>>:"),":<<temp_>>DIN>")
	$result = DYNEVAL($dyn_txt_)
elseif $strfind($dyn_txt_,'(\+{2,4})[^+]+(\+?[^+]+)*\1') = $dyn_txt_ or $strfind($dyn_txt_,'(={2,4})[^=]+(=?[^=]+)*\1')=$dyn_txt_:
	$result = @int.html.Hn($dyn_txt_)
else
	$result = $dyn_txt_
end
$result = $TRIM($result)
--- int.DIN ---------------------------------