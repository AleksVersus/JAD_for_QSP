QSP-Game Интерпретатор инвентаря.
! Выводит список объектов, находящихся в позиции $GAME_VALUE['inventory.floor']
! используется функция get.daughter.obj
# int.inventory
$GAME_VALUE['inventory.floor'] = $iif($GAME_VALUE['inventory.floor'] = '', 'INVENTORY', $GAME_VALUE['inventory.floor'])
killobj
local back_number_, pos_, $name_
if $GAME_INTERFACE['hero.stat']='inventory':
	back_number_ = 2
	addobj $replace($replace(@get.scale.hero(),'<center>'),'</center>')
else:
	back_number_ = 1
end
pos_ = arrpos('$id_array', $GAME_VALUE['inventory.floor'])	&	!@	вычисляем позицию объекта, дочерние объекты которого мы воспроизводим.
$name_ = @em.tag.getCont($object_array[pos_],'name') 
if $position_array[pos_] <> "":
	!@ Если объект, который мы выводим, принадлежит другому объекту, первый предмет в инвентаре должен возвращать нас на предыдущий уровень.
	addobj '<table width=100% cellpadding=0 cellspacing=0 border=0><tr><td align=left><img src="'+@base.img('back')+'"></td><td align=left><font color=#ff0000><b>&nbsp;&nbsp;&nbsp;<<$name_>></b><font><oid:back:oid><back:'+$position_array[pos_]+':back></td></tr></table>','',back_number_
end
local $temp_id_, $obj_	&	!@ защита от дурака
if @get.daughter.obj($GAME_VALUE['inventory.floor'],'', '$temp_id_')='true':
	loop local i = 0 while i<arrsize('$temp_id_') step i += 1:
		$obj_ = $object_array[arrpos('$id_array',$temp_id_[i])]
		if instr($obj_, '[hide]')=0 and instr($obj_, '<property>')=0:
			gs 'int.inventory.obj',$temp_id_[i]
		end
		!@ если вы хотите, чтобы свойства выводились вместе с основными частями тела, удалите из четырнадцатой строки - and instr($obj_,'<property>')=0
	end
end
--- int.inventory ---------------------------------

# int.inventory.obj
! интерпретатор воссоздаёт предмет в инвентаре по ай-ди.
$args[0] = $args[0] 	&	!	id предмета
args['pit']=arrpos('$id_array',$args[0])
if instr($object_array[args['pit']],'<body>')!0:
! Если объект является "частью тела":
	if $include_array[args['pit']]!'':
	! если часть тела имеет дочерний объект
		args['pos']=arrpos('$id_array',$include_array[args['pit']])	&	!	вычисляем позицию дочернего объекта
		$args['oid']=$id_array[args['pos']]
		jump 'incl'
	else
		$args['empty'] = @em.tag.getCont($object_array[args['pit']],'empty')
		addobj $args['empty']+'<oid:'+$args[0]+':oid>' & exit
	end
else
	args['pos']=args['pit']
	$args['oid']=$args[0]
	:incl
	$args['name'] = @em.tag.getCont($object_array[args['pos']],'name')
	$args['name'] = @get.word.padez($args['name'],'И')
	if instr($object_array[args['pos']],'[lock.obj]')!0:
		$args['color']='888888'	&	!	цвет залоченых предметов
	else
		$args['color'] = @em.tag.getNum($object_array[args['pos']],'color')
		if $args['color']='': $args['color']='449900'	&	!	цвет предметов по умолчанию
	end
	if instr($object_array[args['pos']],'<obj>')!0 and kolvo_array[args['pos']]!0: $args['kolvo']=' ('+str(kolvo_array[args['pos']])+')'
end
if $args['name']!'': addobj '<font color=#'+$args['color']+'><b>'+$args['name']+'</b>'+$args['kolvo']+'</font><oid:'+$args['oid']+':oid>'
--- int.inventory.obj ---------------------------------