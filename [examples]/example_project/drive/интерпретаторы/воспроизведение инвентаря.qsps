# int.inventory
! Интерпретатор инвентаря.
! Выводит список объектов, находящихся в позиции $GAME_VALUE['inventory.floor']
! используется функция get.daughter.obj
if $GAME_VALUE['inventory.floor'] = '':
	$GAME_VALUE['inventory.floor']='INVENTORY'
else
	$GAME_VALUE['inventory.floor'] = $GAME_VALUE['inventory.floor']
end
killobj
killvar '$infop'
if $GAME_INTERFACE['hero.stat']='inventory':
	args['back.number']=2
	$args['interface']+=$replace($replace($func('get.scale.hero'),'<center>'),'</center>')
	addobj $args['interface']
else
	args['back.number']=1
end
args['pos']=arrpos('$id_array',$GAME_VALUE['inventory.floor'])	&	!	вычисляем позицию объекта, дочерние объекты которого мы воспроизводим.
$args['name']=$func('em.tag.getCont',$object_array[args['pos']],'name') 
if $position_array[args['pos']]!"":
	! Если объект, который мы выводим, принадлежит другому объекту, первый предмет в инвентаре должен возвращать нас на предыдущий уровень.
	addobj '<table width=100% cellpadding=0 cellspacing=0 border=0><tr><td align=left><img src="'+$func('base.img','back')+'"></td><td align=left><font color=#ff0000><b>&nbsp;&nbsp;&nbsp;'+$args['name']+'</b><font><oid:back:oid><back:'+$position_array[args['pos']]+':back></td></tr></table>','',args['back.number']
end
killvar '$temp_id'	&	! защита от дурака
if $func('get.daughter.obj',$GAME_VALUE['inventory.floor'])='true':
	args['i']=0
	:for
	$args['obj']=$object_array[arrpos('$id_array',$temp_id[args['i']])]
	if instr($args['obj'],'[hide]')=0 and instr($args['obj'],'<property>')=0: gs 'int.inventory.obj',$temp_id[args['i']]
	! если вы хотите, чтобы свойства выводились вместе с основными частями тела, удалите из четырнадцатой строки - and instr($args['obj'],'<property>')=0
	args['i']+=1
	if args['i']<arrsize('$temp_id'): jump 'for'
end
killvar '$temp_id'
--- int.inventory ---------------------------------

# int.inventory.obj
! интерпретатор воссоздаёт предмет в инвентаре по ай-ди.
$args[0] = $args[0] 	&	!	id предмета
args['pit']=arrpos('$id_array',$args[0])
if instr($object_array[args['pit']],'<body>')!0:
! Если объект является "частью тела":
	if $include_array[args['pit']]!'':
	! если часть тела имеет дочерний объект
		args['pos']=arrpos('$id_array',$include_array[args['pit']])	&	!	вычисляем позицию дочернего объекта
		$args['oid']=$id_array[args['pos']]
		jump 'incl'
	else
		$args['empty'] = $func('em.tag.getCont',$object_array[args['pit']],'empty')
		addobj $args['empty']+'<oid:'+$args[0]+':oid>' & exit
	end
else
	args['pos']=args['pit']
	$args['oid']=$args[0]
	:incl
	$args['name'] = $func('em.tag.getCont',$object_array[args['pos']],'name')
	$args['name'] = $func('get.word.padez',$args['name'],'И')
	if instr($object_array[args['pos']],'[lock.obj]')!0:
		$args['color']='888888'	&	!	цвет залоченых предметов
	else
		$args['color'] = $func('get.tag.num',$object_array[args['pos']],'color')
		if $args['color']='': $args['color']='449900'	&	!	цвет предметов по умолчанию
	end
	if instr($object_array[args['pos']],'<obj>')!0 and kolvo_array[args['pos']]!0: $args['kolvo']=' ('+str(kolvo_array[args['pos']])+')'
end
if $args['name']!'': addobj '<font color=#'+$args['color']+'><b>'+$args['name']+'</b>'+$args['kolvo']+'</font><oid:'+$args['oid']+':oid>'
--- int.inventory.obj ---------------------------------