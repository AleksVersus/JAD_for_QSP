QSP-Game Интерпретатор дневника.
! Сохраняем данные из основного окна
$[0]	&	!	ай-ди дневника
# int.diary
local $diary_id_, $mode_ = $args[0], $args[1]
local $text_, $table_
if $mode_='':
	$mode_ = $iif($diary['view'] = '', 'small', $diary['view'])
end
$diary['view'] = $mode_
if $diary['tab'] = '': $diary['tab'] = 'next'
if instr($setScreen, 'diary.') = 0: $diary['setScreen'] = $setScreen
@setScreen('diary.' + $mode_)
if $diary['maintxt'] = '': $diary['maintxt'] = $maintxt
!@ В дневнике несколько вкладок. Активная выделена. Неактивные - нет
!@ Вкладки
!@	info - информация о времени в мире и других важных вещах
!@	next - задания, которые нужно выполнить
!@	last - выполненные задания
!@	eror - проваленные задания
!@	note - заметки
!@	sets - настройки

!@ создаём выводимый текст:
if $diary['tab']='info':
	$text_=$func('int.diary.info')
elseif instr('[next][last][eror]', '['+$diary['tab']+']') <> 0:
	$text_= @int.diary.task($diary_id_, $diary['tab'])
elseif $diary['tab'] = 'note':
	$text_= @int.diary.note($diary_id_)
elseif $diary['tab'] = 'sets':
	$text_= @int.diary.sets($diary_id_)
end
if $GAME_INTERFACE['подсказки'] = "show": $text_ = @game.help($diary['tab']) + @b.w.s('help')+'<br>'+$text_
!@ рисуем таблицу
$table_ = ''
$table_ += $func('int.diary.'+$mode_, $diary_id_,$text_)
*CLEAR
*pl $table_
--- int.diary ---------------------------------