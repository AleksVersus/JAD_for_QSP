! Функция проверяет наличие задания и его соответствие условиям
$[0]	&	!	краткое имя задания
$[1]	&	!	параметр выполнения next last eror
$[2]	&	!	краткое имя стадии выполнения
$[3]	&	!	параметр выполнения стадии next last eror
# prv.task.diary
local $task_kind_, $task_state_, $stage_kind_, $stage_state_ = $args[0], $args[1], $args[2], $args[3]
local $task_id_, pos_, $temp_state_, $stage_id_, $temp_stage_
$task_id_ = @get.id.obj('<note>[\s\S]*(\[:<<$task_kind_>>:\][\s\S]*\[task\]|\[task\][\s\S]*\[:<<$task_kind_>>:\])')
pos_ = arrpos('$id_array', $task_id_)
$temp_state_ = @em.tag.getCont($object_array[pos_], 'v')
if $task_kind_ <> '':
	if $task_id_ <> 'false':
		$result = $iif($task_state_ <> '', $iif($task_state_ = $temp_state_, $task_id_, 'false'), $task_id_)
	else:
		$result = 'false'
		exit
	end
end
$stage_id_ = @get.id.obj('<note>[\s\S]*(\[:<<$stage_kind_>>:\][\s\S]*\[stady\]|\[stady\][\s\S]*\[:<<$stage_kind_>>:\])')
pos_ = arrpos('$id_array', $stage_id_)
$temp_stage_ = @em.tag.getCont($object_array[pos_], 'v')
if $stage_kind_ <> '':
	if $stage_id_ <> 'false':
		$result = $iif($stage_state_ <> '',	$iif($stage_state_ = $temp_stage_, $stage_id_, 'false'), $stage_id_)
	else:
		$result='false'
		exit
	end
end
--- prv.task.diary ---------------------------------
! Внимание!!! Если в качестве условия проверки были указаны стадия и задание, результат false будет выдаваться при отсутствии хотя бы одной составляющей.
! Чтобы производить поиск только по одному из компонент, не указывайте остальные в качестве аргументов
! Результатом работы функции является ай-ди стадии, либо ай-ди задания, если не указана стадия.