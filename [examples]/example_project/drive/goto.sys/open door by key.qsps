QSP-Game
! Локация производит действие вскрытия двери с помощью ранее найденной отмычки
! Используются значения из переменных $key_iag и $int_act_goto[0]
! Чем больше уровень отмычки, мастерства, меньше уровень замка, чем выше удача (как максимальная, так и текущая) тем больше вероятность взлома.
! Взлом вероятен в любом случае, даже если удача на минимуме, а разница между уровнем отмычки и замком невероятная.
! провал взлома так же возможен в любом случае.
! Уменьшаем количество отмычек на 1
# use.key.inDoor
$key_iag = $key_iag
$int_act_goto[0] = $int_act_goto[0]
local cur_luck_, max_luck_, brkn_lvl_, lock_lvl_, key_lvl_
local abs_luck_, brk_, var_
cur_luck_ = property['hero.luck.all']	&	!@	текущая удача
max_luck_ = property['hero.luck.max']	&	!@	максимальная удача
brkn_lvl_ = property['hero.breaking']	&	!@	уровень умения вскрывать замки
lock_lvl_ = @em.tag.getNum($object_array[arrpos('$id_array', $int_act_goto[0])], 'breake')	&	!@	уровень замка
key_lvl_ = @em.tag.getNum($object_array[arrpos('$id_array', $key_iag)], 'breake')	&	!@	уровень отмычки

abs_luck_ = (rand(1,cur_luck_)*100/cur_luck_)-((max_luck_-cur_luck_)*100/max_luck_)
brk_ = (lock_lvl_-(key_lvl_+brkn_lvl_/key_lvl_))*100/lock_lvl_
var_ = rand(-abs_luck_, 100)
if no var_ < brk_:
	*pl 'Замок взломан'
	if instr($object_array[arrpos('$id_array',$int_act_goto[0])],'[locked]')=0:
		@rpl.str.inObj.id($int_act_goto[0], '\[key:[\s\S]+:key\]')
		@rpl.str.inObj.id($int_act_goto[0], '\[breake:[\d]+\]')
	end
else
	*pl 'Неудачная попытка'
end
--- use.key.inDoor ---------------------------------