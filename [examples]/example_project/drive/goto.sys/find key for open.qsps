QSP-Game ! Функция получает ай-ди ключа, которым можно открыть замок
! Объект ключа содержит следующие данные
! <key> [name:Название ключа:name] [key:короткое_имя:key] [open:5] [breake:1000]
! В переменной kolvo_array[] - может быть записано количество подобных ключей. Если оно равно нулю, ключ считается потерянным, его нужно удалить
! В переменной charge_array[] - при создании ключа было вписано значение из [open:5]. Если тег присутствует, а charge_array[]=0, ключ не используется
!поиск производится до тех пор, пока не будет просмотрен весь массив ключей.
! [0] - короткое слово
# get.key.inOpen
local $key_kind_ = $args[0]
loop local pos_ = -1 while 1:
	pos_ = arrcomp('$object_array', '[\s\S]*<key>[\s\S]*', pos_+1)
	if no pos_ < 0:
		if @prv.str.inObj($position_array[pos_],'<keys>') = 'false':	jump 'continue'
		if @em.tag.getCont($object_array[pos_], 'key') = $key_kind_:
			!@ если ключ уже не существует, удаляем его:
			if kolvo_array[pos_] = 0: gosub 'del.obj', $id_array[pos_] & jump 'continue'
			!@ проверяем может ли ключ открывать двери:	
			if @em.tag.getNum($object_array[pos_], 'open')<>0 and charge_array[pos_]=0: jump 'continue'	
			!@ если ключ есть и он может открыть дверь - ай-ди найден:
			$result = $id_array[pos_]
			exit
		end
	else
		exit
	end
	:continue & !@ прерываем итерацию и продолжаем цикл
end
--- get.key.inOpen ---------------------------------