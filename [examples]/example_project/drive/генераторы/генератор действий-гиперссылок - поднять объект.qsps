QSP-Game генерирует действие для поднятия предмета
$[0]	&	!	ай-ди предмета
$[1]	&	!	ай-ди локации
$[2]	&	!	режим:
	- acts - получаем действия в виде динамического кода
	- href - получаем гиперссылки пустое значение - действия добавляются напрямую
$[3]	&	!	различные надстройки. Т.е. какую инфу нам хотелось бы видеть при отображении предмета:
!	[count] - количество
!	[weight] - только при наличии count - вес
!	[take: ] - содержимое тега. Указанное слово или слово по-умолчанию из базы.
!	[cost] - стоимость
!	[spirit] - духовная ценность
# gen.act.obj
local $obj_id_, $loc_id_, $mode_, $sup_sets_ = $args[0], $args[1], $args[2], $args[3]
if $light_array[$loc_id_] = 'темно': exit	&	!@	в темноте нельзя разглядеть предметов, завершаем
local $obj_body_, pos_, $obj_name_, weight_, $number_
local $temp_, temp_, $take_verb_, $price_, $spirit_, $action_name_
!@ ------------------ различные свойства предметов ----------
	$obj_body_ = @get.obj.id($obj_id_)
	pos_ = arrpos('$id_array', $obj_id_)
	$obj_name_ = @em.tag.getCont($obj_body_, 'name')
	$obj_name_ = @get.word.padez($obj_name_, 'В')
	weight_ = @em.tag.getNum($obj_body_, 'weight')
	if kolvo_array[pos_] <> 0 and instr($sup_sets_,'[count]') <> 0:
		$number_ = ' (<<kolvo_array[pos_]>> штук<<@get.word.end(kolvo_array[pos_]>>а|и|')
	end
	if weight_ <> 0 and instr($sup_sets_,'[weight]') <> 0:
		$number_ += ' x '+@em.indiv(weight_,100,10)+')'
	else:
		$number_ += ')'
	end
!@ ------------------ различные свойства предметов ----------
!@ слово подъёма
$temp_ = @em.tag.getNum($sup_sets_, 'take')
if $temp_ = '' or $temp_ = 'take':
	$take_verb_ += @b.d.t('take')
elseif $temp_ = 'lift':
	$take_verb_ += @b.d.t('lift')
elseif $temp_ = 'buy':
	$take_verb_ += @b.d.t('buy')
else:
	$take_verb_ += $temp_
end
!@ стоимость cost:
if instr($sup_sets_,'[cost]')<>0:
	temp_ = @em.tag.getNum($obj_body_,"stoim")
	$temp_ = @em.indiv(@obj.purc.cost(temp_, $loc_id_), 100, 1)
	!@	заменить на обращение к базе
	$price_ = ' по <<$temp_>> дублон' + @get.word.end(val($temp_),'|а|ов') + '.'
end
!@ духовная ценность:
if instr($sup_sets_,'[spirit]')<>0:
	temp_ = @get.spirit($obj_id_,$loc_id_)
	$spirit_ = " (<<@em.indiv(temp_, 100, 10)>> ед. кармы) "
else
	$spirit_ = ""
end
!@ формируем название действия из всего вышеперечисленного
$action_name_ = $take_verb_ + $obj_name_ + $number_ + $price_ + $spirit_

if $mode_ = 'href' or $mode_ = '':
	if instr($obj_body_, '[lock-obj]') = 0:
		$result += @const.10SP + "<a class=""plain"" href=""exec:@take.obj('<<$obj_id_>>') & @true.goto.curloc($curloc)""><font color=#880088><<$action_name_>></font></a><br>"
	else:
		$result += @const.10SP + "<a class=""plain"" href=""exec:$print['take.obj'] += @b.w.s('loc-obj') & @true.goto.curloc($curloc)""><font color=#888888><<$action_name_>></a></font><br>"
	end
elseif $mode_ = 'acts':
	if instr($obj_body_, '[lock-obj]') = 0:
	$result = "
		act '<font color=#880088><<$action_name_>></font>':
			@take.obj('<<$obj_id_>>')
			@true.goto.curloc($curloc)
		end"
	else:
	$result = "
		act '<font color=#888888><<$action_name_>></font>':
			$print['take.obj'] += @b.w.s('loc-obj')
			@true.goto.curloc($curloc)
		end"
	end
end
--- gen.act.obj ---------------------------------
