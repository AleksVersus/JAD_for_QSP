QSP-Game Отвечает за подсчёт удачи сил героя
[0]	&	!	на сколько изменяется
[1]	&	!	в какую сторону изменяется
[2] &	!	all - текущий запас max - максимальный уровень
[3]	&	!	дополнительный текст
# hero.luck
local change_, $sign_, $level_, $add_text_ = args[0], $args[1], $args[2], $args[3]
if $level_='': $level_='all'
if change_ <> 0:
	local $prop_ = 'luck'
	local $tl_[0], $tl_[1], $tl_[2], $tl_[3] = '025.3', '025.4', '025.1', '025.2'
	local last_max_ = property['hero.<<$prop_>>.max']
	if $sign_ = '' or $sign_ = 'del' or $sign_ = '-':
		property['hero.<<$prop_>>.' + $level_] -= change_
		if $level_ = 'all': $print['hero.health'] += @base.word.screen($tl_[0],'[колво:<<change_>>] [txt:<<$add_text_>>:txt]')
		if $level_ = 'max': $print['hero.health'] += @base.word.screen($tl_[1],'[колво:<<change_>>] [txt:<<$add_text_>>:txt]')
	elseif $sign_ = '+' or $sign_ = 'add':
		property['hero.<<$prop_>>.' + $level_] += change_
		if $level_ = 'all': $print['hero.health'] += @base.word.screen($tl_[2],'[колво:<<change_>>] [txt:<<$add_text_>>:txt]')
		if $level_ = 'max': $print['hero.health'] += @base.word.screen($tl_[3],'[колво:<<change_>>] [txt:<<$add_text_>>:txt]')
	end
	if property['hero.<<$prop_>>.max'] <> last_max_: property['hero.<<$prop_>>.all'] = property['hero.<<$prop_>>.all'] + (property['hero.<<$prop_>>.max'] - last_max_)
	!@ Удача может превышать максимальный уровень.
	if property['hero.<<$prop_>>.all'] < 5: property['hero.<<$prop_>>.all'] = 5
	$print['hero.health'] += '<font size=-1>' + @int.hero.property(@get.id.obj('<property>[\s\S]*\[:<<$prop_>>:\]')) + '</font>'
end
--- hero.luck ---------------------------------