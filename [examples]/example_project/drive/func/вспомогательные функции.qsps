QSP-Game Вспомогательные функции

! функция получает падеж из строки вида Именительныq|Родительный|Дательный|Винительный|Творительный|Предложный
$args[0] = $args[0]	&	!	строка
$args[1] = $args[1]	&	!	падеж
# get.word.padez
local $string_, $case_ = $args[0], $args[1]
if $case_ = 'И' or $case_ = '':
	$result = @em.str.getWord($string_, 1) & exit
elseif $case_ = 'Р':
	$result = @em.str.getWord($string_, 2) & exit
elseif $case_ = 'Д':
	$result = @em.str.getWord($string_, 3) & exit
elseif $case_ = 'В':
	$result = @em.str.getWord($string_, 4) & exit
elseif $case_ = 'Т':
	$result = @em.str.getWord($string_, 5) & exit
elseif $case_ = 'П':
	$result = @em.str.getWord($string_, 6) & exit
end
--- get.word.padez ---------------------------------


! вычисляет окончание по числу.
! args[0] - число
! $args[1] - окончания в формате a|b|c
! можно писать окончание для нулевого варианта отдельно! 1 очко|2 очка|0 или 5 очков|ноль
# get.word.end
local number_, $ending_, units_, tens_ = args[0], $args[1], args[0] mod 10, args[0] mod 100
if number_ < 0: number_ = -number_
$result = 'gwe_error'
if units_ = 1 and tens_ <> 11:
	$result = @em.str.getWord($ending_, 1) & exit
end
if units_ > 1 and units_ < 5 and (tens_ < 5 or tens_ > 20):
	$result = @em.str.getWord($ending_, 2) & exit
end
if units_ = 0 or units_>4 or (tens_>10 and tens_<21):
	$result = @em.str.getWord($ending_, 3)
end
local $four_ending_ = @em.str.getWord($ending_, 4)
if number_ = 0 and $four_ending_ <> '' and $four_ending_ <> $result:
	$result = $four_ending_
end
--- get.word.end ---------------------------------

! Локация склеивает две строки с числовыми тегами.
$args[0] = $args[0]
$args[1] = $args[1]
# remastering.tag.number
loop local u, a = 1, 0 while a < 2 step a += 1:
	loop local i, $s = 1, '' while $strfind($args[a],"\[[\d]+:[\s\S]*:[\d]+\]")<>'' step i += 1:
		$s = $strfind($args[a],"\[<<i>>:([\s\S]+):<<i>>\]", 1)
		if $s <> '':
			$result += "[<<u>>:<<$s>>:<<u>>]"
			u += 1
		end
		$args[a] = $TRIM($replace($args[a],"[<<i>>:<<$s>>:<<i>>]"))
	end
end
--- remastering.tag.number ---------------------------------

# get.loc.id
if $curloc = '[chest]' or ($cvar['loc_id' + $cvar['chest']]<>'' and $strfind($curloc,'(\[м:[\d]+\]_[\s\S]+|место;|place;)')<>''):
	$result = $cvar['loc_id'+$cvar['chest']]
else
	$result = $curloc
end
--- get.loc.id ---------------------------------