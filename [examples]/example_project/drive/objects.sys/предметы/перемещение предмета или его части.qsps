QSP-Game ! Перемещает часть предмета или весь предмет в указанную позицию
! Результатом является ай-ди перемещённого предмета
$[0]	&	!	ай-ди предмета
$[1]	&	!	ай-ди объекта, который должен стать родительским
$[2]	&	!	ключевое слово
[3]		&	!	количество перемещаемых предметов. Внимание!!! может быть равно 0
[4]
# replace.obj
local $obj_id_, $place_id_, $cmd_, how_many_ = $args[0], $args[1], $args[2], args[3]
if $cmd_ = '': $cmd_ = $mid($obj_id_, 1, instr($obj_id_, '.')-1)
local pos_, number_, place_pos_, $t
pos_ = arrpos('$id_array',$obj_id_)
!@ позиция в базе
number_ = kolvo_array[pos_]
!@ количество предметов в позиции
if number_ < how_many_: how_many_ = number_
!@ Если в позиции меньше предметов, чем хочется переместить, Количество перемещаемых предметов приравнивается количеству предметов в позиции
place_pos_ = @get.similar.obj($obj_id_, $place_id_)	&	!@	находит идентичный объект в указанном месте
if number_>how_many_:
!@ переносится часть объекта
	local charge_, $run_code_, out_number_
	if charge_array[pos_] > 0: charge_ = charge_array[pos_]
	if $run_array[pos_] <> '': $run_code_ = $run_array[pos_]
	if how_many_ > 0: out_number_ = how_many_
	if place_pos_ = -1:
		$result = @add.new.obj($object_array[pos_], $place_id_, $cmd_, out_number_, charge_, $run_code_)
	else
		kolvo_array[place_pos_] += how_many_
		$result = $id_array[place_pos_]
	end
	kolvo_array[pos_] -= how_many_
else:
!@ переносится весь объект
	if place_pos_=-1:
		@clr.parent.obj($obj_id_)	&	!@	удаляются все связи с родительскими объектами
		$position_array[pos_]=$place_id_
		$result = $obj_id_
	else
		kolvo_array[place_pos_]+=how_many_
		$result = $id_array[place_pos_]
		@del.obj($obj_id_)
	end
end
--- replace.obj ---------------------------------

! Результатом может быть:
! ай-ди нового объекта - если идентичного объекта в указанном месте нет и переносится часть объекта
! ай-ди идентичного объекта - если идентичный объект в указанном месте найден
! ай-ди исходного объекта - если идентичный объект не найден и переносится весь объект
