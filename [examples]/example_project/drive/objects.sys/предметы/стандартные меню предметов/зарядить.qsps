# menu.obj.inCharge
! локация зарядки ружья. Выполняет сдвоенную функцию:
! выбирает списком все ружья, которые есть у героя и предлагает их в виде ссылок
! если обращение происходит от ссылки, помещает заряд в ружьё, если это возможно
if $args[0]='': $args[0]=$OOS['oid']
$args[1] = $args[1]
args['pit.patron']=arrpos('$id_array',$args[0])
! получаем название патронов
$args['name.patron']=$func('get.tag.cont',$object_array[args['pit.patron']],"name")
$args['patron']=$func('get.tag.cont',$object_array[args['pit.patron']])
args['weight.patron']=func('get.tag.num',$object_array[args['pit.patron']],'weight')
! получаем калибр и прочую информацию
$args['calibr.patron']=$func('get.tag.num',$object_array[args['pit.patron']],'calibr')
args['maxCharge.patron']=func('get.tag.num',$object_array[args['pit.patron']],'maxchrg')
if $args[1]='':
! находим все объекты
	$args['get.arms']=$func('get.daughter.obj.all','INVENTORY','','$id_moic')
	if $args['get.arms']='true':
		:Game_Adventures_Making
		if arrsize('$id_moic')>0:
				args['pit.arm']=arrpos('$id_array',$id_moic[0])
				$args['arm']=$object_array[args['pit.arm']]
			if $strfind($args['arm'],'<obj>[\s\S]*\[огнестрельное\]')!"":
				$args['calibr.arm']=$func('get.tag.num',$args['arm'],'calibr')
				args['maxCharge.arm']=func('get.tag.num',$args['arm'],'maxchrg')
				if $args['calibr.arm']=$args['calibr.patron'] and args['maxCharge.patron']<=args['maxCharge.arm']-charge_array[args['pit.arm']]:
					$args['name.arm']=$func('get.word.padez',$func('get.tag.cont',$args['arm'],"name"),'В')
					$args['res.021213']='true'
					args['u']+=1
					!начало генерации ссылки для вывода на экран:
					$print['menu.else']+="[<<args['u']>>]: "	&	!	просто номер надписи по порядку
					! проверяем родительский объект на наличие слова <body>
					if instr($object_array[arrpos('$id_array',$position_array[args['pit.arm']])],'<body>')!0: $args['menu.else']="*" else $args['menu.else']=""
					$print['menu.else']+='<a class="plain" href="exec:'+"gosub 'menu.obj.inCharge','"+$args[0]+"','"+$id_moic[0]+"'"+'">Зарядить '	&	!	начало ссылки
					$print['menu.else']+=$func('get.word.padez',$args['name.patron'],'В')+" в "+$args['name.arm']+$args['menu.else']+" [<<charge_array[args['pit.arm']]>>/<<args['maxCharge.arm']>>]"	&	!	конец ссылки
					$print['menu.else']+=".</a><br>"
					! конец генерации ссылки для вывода на экран:
				end
			end
			killvar '$id_moic',0
			jump 'Game_Adventures_Making'
		end
	end
	if $args['res.021213']!'true': $print['menu.else']+=$func('base.word.screen','003.1') else $print['menu.else']+=$func('b.w.s','u')
else
	args['pit.arm']=arrpos('$id_array',$args[1])
	$args['arm']=$object_array[args['pit.arm']]
	$args['calibr.arm']=$func('get.tag.num',$args['arm'],'calibr')
	args['maxCharge.arm']=func('get.tag.num',$args['arm'],'maxchrg')
	$args['name.arm']=$func('get.word.padez',$func('get.tag.cont',$args['arm'],"name"),'В')
	! помещаем патронташ в пистолет
	args['штук']=charge_array[args['pit.patron']]/args['maxCharge.patron']	&	!	всегда должно быть равно 1
	$args['new.arm.charge']=$func('redo.obj.inCharge',$args[1],$position_array[args['pit.arm']],charge_array[args['pit.patron']],'+',$args[0],args['штук'])
	! в последнюю переменную внесен ай-ди оружия, которое получилось в результате. Этому оружию передаётся вес патронов
	args['pit.arm']=arrpos('$id_array',$args['new.arm.charge'])
	$args['arm']=$object_array[args['pit.arm']]
	args['weight.arm']=func('get.tag.num',$args['arm'],'weight')
	$object_array[args['pit.arm']]=$replace($object_array[args['pit.arm']],"[weight:<<args['weight.arm']>>]","[weight:<<args['weight.arm']+args['weight.patron']*args['штук']>>]")
	$print['menu.else']+='<font color=#008888><b>'+$func('get.word.padez',$args['name.patron'],'В')+" заряжены в "+$args['name.arm']+'.</b></font>'
	gs 'int.inventory'
	pl $func('print.word')
end
killvar '$id_moic'
--- menu.obj.inCharge ---------------------------------