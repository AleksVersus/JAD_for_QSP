QSP-Game локация разбирает предмет на составляющие части
! Фактически: получает список составляющих по предмету, создаёт на их основе новые предметы
# menu.obj.unmount
local $obj_id_ = $args[0]
if $obj_id_ = '': $obj_id_ = $OOS['oid']
local $obj_body_, pos_, $obj_kind_, weight_, $unmount_list_, $flop_, $temp_
$obj_body_ = @get.obj.id($obj_id_)	&	!	получаем тело предмета
pos_ = arrpos('$id_array',$obj_id_)
$obj_kind_ = @em.tag.getCont($obj_body_)
weight_ = @em.tag.getNum($obj_body_,'weight')
$unmount_list_ = @base.new.obj('[:<<$obj_kind_>>:]','unmount')
if $unmount_list_ = 'cant unmount':
	$flop_ = $iif($strfind($run_array[pos_],'!cant.unmount!') <> '', @run.dynamic.script('!cant.unmount!', $obj_id_), 'cant unmount')
	if $flop_='cant unmount':
		$print['obj.unmount'] += @base.word.screen('023')
		jump 'print'
	end
end
local unmount_iner_, $unmount_name_, $cut_str_, unmount_weight_, $ingr_id_
unmount_iner_ = iif(kolvo_array[pos_] > 5, $input(@b.d.t('rebuild',kolvo_array[pos_])), 1)
if unmount_iner_ > 0 and no unmount_iner_ > kolvo_array[pos_]:
	loop local i = 1 while $strfind($unmount_list_,"\[[\d]+:[\s\S]*:[\d]+\]") <> '' step i += 1:
		$cut_str_ = $replace($replace($strfind($unmount_list_, "\[<<i>>:[\s\S]+:<<i>>\]"), "[<<i>>:"), ":<<i>>]")
		$unmount_name_ = @em.tag.getCont($cut_str_, 'name')
		unmount_weight_ = @em.tag.getNum($cut_str_, 'count')
		$ingr_id_ = @base.new.obj('[:<<$unmount_name_>>:]', '', 'INVENTORY', unmount_iner_ * unmount_weight_)
		$temp_ = @replace.obj($ingr_id_, $cvar['loc_id'+$cvar['chest']], $obj_kind_, unmount_iner_ * unmount_weight_)
		$unmount_list_ = $trim($replace($unmount_list_,"[<<i>>:<<$cut_str_>>:<<i>>]"))
	end
	$print['obj.unmount'] += @base.word.screen('024', '<<$obj_body_>> [колво:<<unmount_iner_>>]')
	@minus.obj($obj_id_, unmount_iner_)
	property['hero.power.all'] -= unmount_iner_ * weight_
	$print['power.change'] += @int.hero.property(@get.id.obj('<property>[\s\S]*\[:power:\]'))
else
	$print['obj.unmount'] += @base.word.screen('022')
end
:print
@int.inventory
@true.goto.curloc($curloc)
--- menu.obj.unmount ---------------------------------