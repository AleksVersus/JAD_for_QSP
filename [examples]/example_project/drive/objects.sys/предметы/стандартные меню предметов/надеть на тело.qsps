QSP-Game локация проверяет возможность поместить предмет в указанную позицию на теле и помещает его туда, если есть такая возможность.
# menu.in.body
local $obj_id_, $temp_ = $args[0], ''
if $obj_id_ = '': $obj_id_ = $OOS['oid']
local pos_, $obj_body_, weight_, number_, how_many_, $use_, w
pos_ = arrpos('$id_array',$obj_id_) 	&	!@	получаем позицию предмета в базе
$obj_body_ = @get.obj.id($obj_id_)	&	!@	получаем тело предмета
weight_ = @em.tag.getNum($obj_body_,'weight')	&	!@	получаем вес предмета
number_ = kolvo_array[pos_]	&	!@ запоминаем текущее количество
if instr($obj_body_, '[number]') <> 0:
	how_many_ = 1
elseif  instr($obj_body_, '[count]')<> 0:
	how_many_ = number_
end
$use_ = @use.obj.actObj($obj_id_,'dont.pay',how_many_)	&	!@	перемещаем объект если это возможно
if $use_ = 'taken':
	!@ Предмет взят полностью. Значит вес уменьшается на полное число
	w = number_ * weight_
elseif $use_ = 'dont take':
	w = 0
	$print['menu.in.body'] = @base.word.screen('003')
	$temp_ = @em.tag.getCont($obj_body_,'pos')
	$print['use.obj.actObj'] = @b.w.s('full.pos',$temp_ )
else:
	!@ предмет взят, но не полностью, значит возвращён остаток
	w = weight_ * (number_ - @em.tag.getNum($use_, 'колво'))
end
property['hero.power.all'] -= w
$print['power.change'] += @int.hero.property(@get.id.obj('<property>[\s\S]*\[:power:\]'))
if $strfind($run_array[arrpos('$id_array',$infop['last_id'])],'!outbag!') <> '':
	if $infop['taked'] = 'true': $temp_ = @run.dynamic.script('!outbag!',$infop['last_id'],$infop['past_id'])
end
@int.inventory()
--- menu.in.body ---------------------------------