QSP-Game выгружает из оружия все заряды
# menu.obj.disCharge
local $obj_id_ = $args[0]
if $obj_id_ = '': $obj_id_ = $OOS['oid']
local pos_, $id_modc_, $temp_, $bag_id_, pit_, weight_decr_, number_
local $replaced_, $features_set_, $output_text_, weight_
pos_ = arrpos('$id_array', $obj_id_)
!@ действует следующим образом:
!@ 1. Получаем список дочерних объектов
$temp_ = @get.daughter.obj($obj_id_,'','$id_modc_')
!@ вычисляем рюкзак
loop while arrsize('$id_modc_')>0 step killvar '$id_modc_',0:
	!@ каждый дочерний предмет просто перемещается в рюкзак
	$bag_id_ = @get.bag($id_modc_[0])
	pit_ = arrpos('$id_array',$id_modc_[0])
	weight_decr_ += @em.tag.getNum($object_array[pit_],'weight')*kolvo_array[pit_]
	number_ = kolvo_array[pit_]
	$replaced_ = @replace.obj($id_modc_[0], $bag_id_, '', kolvo_array[pit_], 1)
	$features_set_ = $object_array[arrpos('$id_array', $replaced_)]
	$output_text_ += '<font color=#'+@em.tag.getNum($features_set_, 'color')+'>'+@get.word.padez(@em.tag.getCont($features_set_,'name'),'И')+" (<<number_>> шт.)</font>, "
end
!@ заряд оружия очищается
charge_array[pos_] = 0
!@ уменьшаем вес
weight_ = @em.tag.getNum($object_array[pos_], 'weight')
$object_array[pos_] = $replace($object_array[pos_], "[weight:<<weight_>>]", "[weight:<<weight_-weight_decr_)>>]")
$print['menu.else'] += @b.w.s('dch', $obj_id_, $output_text_, $bag_id_)
--- menu.obj.disCharge ---------------------------------