QSP-Game Локация убирает предмет в наиболее подходящую сумку в инвентаре
$[0] - object id
# menu.in.bag
local $obj_id_ = $args[0]
if $obj_id_ = '': $obj_id_ = $OOS['oid']
local pos_, $obj_body_, weight_, number_, $obj_kind_, $bag_id_, $place_id_
local $last_id_, w, $temp_
pos_ = arrpos('$id_array',$obj_id_) 	&	!@ получаем позицию предмета в базе
$obj_body_ = @get.obj.id($obj_id_)	&	!@ получаем тело предмета
weight_ = @em.tag.getNum($obj_body_,'weight')	&	!@ получаем вес предмета
number_ = kolvo_array[pos_]	&	!@ запоминаем текущее количество
$obj_kind_ = @em.tag.getCont($obj_body_)
$bag_id_ = @get.bag($obj_id_)
!@ Необходимо проверить из какого места перемещается предмет, т.е. проверяем класс родительского объекта <body> или <inventory>
$place_id_ = @get.obj.id($position_array[pos_]) 	&	!@ получаем тело родительского объекта
if instr($place_id_,'<inventory>') <> 0:
	w = 0
	$last_id_ = @replace.obj($obj_id_,$bag_id_,$obj_kind_,number_)
	$print['menu.in.bag'] += @base.word.screen('006', $obj_body_+'[колво:<<number_>>]')
elseif instr($place_id_,'<body>') <> 0:
	local how_many_ = @prv.obj.weight($obj_id_,'inBag')	&	!@ получаем сколько можно отправить в сумку
	if how_many_ <> 0:
		$last_id_ = @replace.obj($obj_id_, $bag_id_, $obj_kind_, how_many_)
		w = how_many_ * weight_
		local pit_ = arrpos('$id_array', $last_id_)
		if $run_array[pit_] <> '':
			if $strfind($run_array[pit_],'!deused!') <> '':	$temp_ = @run.dynamic.script('!deused!', $last_id_, $obj_id_)
			if $strfind($run_array[pit_],'!inbag!') <> '':	$temp_ = @run.dynamic.script('!inbag!', $last_id_, $obj_id_)
		end
		$print['menu.in.bag'] += @base.word.screen('004', $obj_body_+'[колво:<<how_many_>>]')
	else
		$print['menu.in.bag'] += @base.word.screen('005')
	end
end
property['hero.power.all'] += w
$print['power.change'] += @int.hero.property(@get.id.obj('<property>[\s\S]*\[:power:\]'))
@int.inventory
--- menu.in.bag ---------------------------------
