QSP-Game функция осуществляет одевание предмета на тело, если есть такая возможность
В результат выдаётся три варианта:
- dont take	- предмет не был взят
- taken		- предмет взят полностью, т.е. всё количество
- другое	- предмет взят не полностью, т.е. только некоторое количество
$[0]	-	ай-ди предмета
$[1]	-	управляющее или сопроводительное слово dont.pay - по этому значению определяем, что не нужно проверять стоимость.
[2]		-	количество
# use.obj.actObj
local $obj_id_, $cmd_, number_, $temp_ = $args[0], $args[1], args[2], ''
if number_ = 0: number_ = 1	&	!@ количество
local $obj_body_, pos_, $obj_kind_, $pos_list_, $bp_
$obj_body_ = @get.obj.id($obj_id_)	&	!@ получаем тело предмета
pos_ = arrpos('$id_array',$obj_id_)	&	!@ получаем позицию предмета в базе
$obj_kind_ = @em.tag.getCont($obj_body_)	&	!@ короткое имя предмета, ай-ди вида.
$result = 'dont take'
$pos_list_ = @em.tag.getCont($obj_body_,'pos')	&	!@ вычисляем список позиций, в которые добавляется указанный предмет
local $array_body_part_, $array_body_part_obj_
$bp_ = @prv.pos.inObj($obj_id_)
if $pos_list_ <> '' and $bp_ = 'empty':
	!@ если предмет должен добавляться на тело и "части тела" свободны
	!@ есть список позиций и заполненный массив $array_body_part_
	!@ Заполняем массив $array_body_part_obj_ объектами "части тела"
	loop local i = 0 while i < arrsize('$array_body_part_') step i += 1:
		$array_body_part_obj_[i] = @get.obj.id($array_body_part_[i])
	end
	local $position_, outpos_, $last_id_
	if @em.tag.getCont($pos_list_,'t1') <> '': $type_bp_ = 't'	&	!@	тип позиции "рука" "перчатки"
	if @em.tag.getCont($pos_list_,'c1') <> '': $type_bp_ = 'c'	&	!@	класс позиции "arm" "dosp"
	if @em.tag.getCont($pos_list_,'p1') <> '': $type_bp_ = 'p'	&	!@	конкретная позиция "правая рука" "левая рука"
	loop local p_ = 1 while len($pos_list_)>0 step p_ += 1:
		$position_ = @em.tag.getCont($pos_list_, $type_bp_+$str(p_))
		!@ получаем очередную позицию из списка
		if $position_ <> '':
		!@ если позиция не равна пустому значению, ищем её в массиве $array_body_part_obj_
			outpos_ = arrcomp('$array_body_part_obj_', '[\s\S]*<<$position_>>[\s\S]*')
			if outpos_ <> -1:
			!@ Если позиция в массиве найдена
				if $last_id_ = '':
				!@ Перемещаем предмет, в позицию "части тела". Заметьте!@ Один раз
					local $is_pay_
					if $strfind(@get.loc.id(),'(\[т:[\d]+\]_[\S\s]+|торговец;|seller;)') <> '' and $cmd_ <> 'dont.pay':
						$is_pay_ = @prv.obj.pay($obj_id_,number_,'purchase')
						if $is_pay_='false':
							$result='dont money'
							exit
						end
					end
					$last_id_ = @replace.obj($obj_id_,$array_body_part_[outpos_],$obj_kind_,number_)
					$result = @prv.obj.id($obj_id_,$last_id_)
					last_id_['pos'] = arrpos('$id_array', $last_id_)
					$infop['last_id'] = $last_id_
					$infop['past_id'] = $obj_id_
					$infop['taked'] = 'true'
					if $is_pay_ = 'true': $infop['pay'] = 'true'
					if $strfind($run_array[last_id_['pos']],'!used!') <> '':
					!@ динамический код, выполняемый при использовании/одевании
						$temp_ = @run.dynamic.script('!used!',$last_id_,$obj_id_)
					end
				end
				$include_array[arrpos('$id_array',$array_body_part_[outpos_])]=$last_id_
				local $bp_kind_ = '[:'+@em.tag.getCont($array_body_part_obj_[outpos_])+':]'
				$print['use.obj.actObj'] += @base.new.bodypart($bp_kind_, 'take.obj:<<$last_id_>>:take.obj')
				!@ Придаём части тела дочерний объект
				
				!@ Удаляем "использованную" "часть тела"
				killvar '$array_body_part_obj_', outpos_
				killvar '$array_body_part_', outpos_
				!@ Вырезаем значение из позиционного листа
				$pos_list_= $trim($replace($pos_list_,$type_bp_+$str(p_)+':'+$position_+':'+$type_bp_+$str(p_)))
			else
				$error_log+='<font size=+1 color=#ff0000><b>ВНИМАНИЕ!!! Произошла ошибка добавления предмета в "части тела" на локации use.obj.actObj Несоответствие начального и конечного списков позиций.</b></font>'
				exit
			end
		end
	end
end
--- use.obj.actObj ---------------------------------
