! локация находит для предмета наиболее подходящую сумку
$[0]	&	!	ай-ди предмета
$[1]	&	!	регулярное выражение, например класс объекта. по умолчанию производится поиск среди сумок
# get.bag
local $obj_id_, $regexp_ = $args[0], $args[1]
if $regexp_ = '': $regexp_ = '<bag>'
local $obj_body_
$obj_body_ = @get.obj.id($obj_id_)	&	!@	получаем тело предмета
!@ -------------- алгоритм поиска подходящей сумки ------------------------------
	$result='INVENTORY'	&	!@	по умолчанию все предметы помещаются в инвентарь, если не будет найдено иного подходящего контейнера.
	!@ ------------------ поиск сумки без целевого слова ------------------------
		loop local pos_ = -1 while 1:
			pos_ = arrcomp('$object_array', '[\s\S]*<<$regexp_>>[\s\S]*', pos_+1)
			!@ Вычисляем первую попавшуюся сумку
			if no pos_ < 0:
				if $strfind($object_array[pos_], '\[:[^:]*]') <> '':
				!@ Если содержит целевое слово, повторяем поиск
					jump 'continue_1'
				else:
				!@ Если не содержит целевого слова - сумка подходящая
					$result = $id_array[pos_]
					jump 'break_1'
				end
			end
			:continue_1
		end
		:break_1
	!@ ------------------ поиск сумки без целевого слова ------------------------
	!@ ------------------ поиск сумки с подходящим целевым словом ---------------
		!@ Получаем список целевых слов
		local $target_list_
		$target_list_ = $replace($replace(@em.tag.getCont($obj_body_,'np'),'[','|'),']')
		$target_list_ = $mid($TRIM($target_list_),2)+'| '
		loop local t, $boob_, pos_ = 1, '', -1 while 1 step t += 1:
			$boob_ = $TRIM(@em.str.getWord($target_list_, t))
			!@ $args['print']+=$boob_+' <<args["tl"]>><br>'
			if $boob_ <> '':
				pos_ = arrcomp('$object_array','[\s\S]*<<$regexp_>>[\s\S]*\[:<<$boob_>>\][\s\S]*|[\s\S]*\[:<<$boob_>>\][\s\S]*<<$regexp_>>[\s\S]*')
				if pos_ <> -1:
					$result = $id_array[pos_]
					jump 'break_2'
				end
			end
		end
		:break_2
	!@ ------------------ поиск сумки с подходящим целевым словом ---------------
	!@ Как видно из последовательности действий, если небудет найдена искомая сумка, предмет будет добавлен в сумку выше по иерархии
!@ -------------- алгоритм поиска подходящей сумки ------------------------------
--- get.bag ---------------------------------