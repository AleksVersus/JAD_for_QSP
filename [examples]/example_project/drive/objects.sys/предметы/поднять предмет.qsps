QSP-Game локация поднимает предмет с текущего положения и добавляет его в инвентарь или рюкзак
$[0]	&	!@ ай-ди предмета
# take.obj
local $obj_id_ = $args[0]
local $temp_, $obj_body_, pos_, $obj_kind_
local how_many_, $last_id_, $take_or_buy_, $on_body_
$obj_body_ = @get.obj.id($obj_id_)	&	!@ получаем тело предмета
pos_ = arrpos('$id_array', $obj_id_)	&	!@ получаем позицию предмета в базе
$obj_kind_ = @em.tag.getCont($obj_body_)	&	!@ короткое имя предмета, ай-ди вида.
!@ выбираем текст подъёма, покупки
$take_or_buy_ = $iif($strfind(@get.loc.id(),'(\[т:[\d]+\]_[\S\s]+|торговец;|seller;)') <> '', '[buy]', '[take]')
if instr($obj_body_,'[oneself]') <> 0:
	!@ здесь может быть проверка на наличие этого предмета.
	$object_array[pos_] = $replace($object_array[pos_], '<obj>', '<'+$obj_kind_+'>')
	kolvo_array[pos_] = 0
	$temp_ = @replace.obj($obj_id_,'INVENTORY')
	$print['take.obj'] += @b.w.s("take.oneself "+$take_or_buy_, $obj_body_)
elseif instr($obj_body_, '[nocount]') <> 0:
!@ Находим подходящий кошелёк. Динамический код при поднятии неисчислимого предмета выполняется с локации add.obj.noucount
	$result = @add.obj.nocount($obj_id_)
elseif $strfind($obj_body_,'\[count\]|\[number\]') <> '':
!@ Если предмет штучный, или меновой. Динамический код надевания предмета выполняется с локации use.obj.actObj
	$on_body_ = $iif(instr($obj_body_,'[count]') = 0, @use.obj.actObj($obj_id_), 'dont take')
	if $on_body_ = 'dont money':
		$result = 'dont take'
		jump 'print'
	elseif $on_body_ <> 'dont take':
		$result = $on_body_
		$print['take.obj'] += @b.w.s("take.number "+$take_or_buy_, $obj_body_+'[колво:1]')
	else
	!@ Если предмет напрямую не одевается на тело, должна быть произведена проверка, можно ли добавить предмет в рюкзак или инвентарь
		!@ Проверяем правильно ли указано количество предметов, которое мы хотим поднять
		how_many_ = @prv.obj.weight($obj_id_)
		if how_many_ <> 0:
		!@ Количество предметов, которые мы собираемся поднять, не выходит за указанные пределы
			if $strfind(@get.loc.id(),'(\[т:[\d]+\]_[\S\s]+|торговец;|seller;)') <> '':
				local $is_pay_ = @prv.obj.pay($obj_id_,how_many_,'purchase')
				if $is_pay_ = 'true': $infop['pay'] = 'true' else jump 'print'
			end
			local weight_, $bag_id_
			weight_ = @em.tag.getNum($obj_body_,'weight')	&	!@ вес одного предмета
			$bag_id_ = @get.bag($obj_id_)
			!@ Перемещаем предмет в полученную позицию
			$last_id_ = @replace.obj($obj_id_, $bag_id_, $obj_kind_, how_many_)
			$result = @prv.obj.id($obj_id_, $last_id_)
			property['hero.power.all'] += how_many_ * weight_
			$print['power.change'] += @int.hero.property(@get.id.obj('<property>[\s\S]*\[:power:\]'))
			$print['take.obj'] += @b.w.s("take.count <<$take_or_buy_>>", '<<$obj_body_>>[колво:<<how_many_>>]')
			last_id_['pos'] = arrpos('$id_array',$last_id_)
			$infop['last_id'] = $last_id_
			$infop['past_id'] = $obj_id_
			$infop['taked'] = 'true'
			if $strfind($run_array[last_id_['pos']],'!inbag!') <> '':
			!@ динамический код помещения при помещении в сумку/инвентарь
				$temp_ = @run.dynamic.script('!inbag!', $last_id_, $obj_id_)
			end
		else:
			!@ Количество предметов, которое мы собираемся поднять, не входит в указанные пределы, либо невозможно поднять ни одного предмета
			$result = 'dont take'
			$print['power.change'] += @int.hero.property(@get.id.obj('<property>[\s\S]*\[:power:\]'))
			$print['take.obj'] += @base.word.screen('001')
		end 
	end
end
!@ $args['bag'] = $func('get.id.obj','<bag>')	&	!@ получаем ай-ди сумки
!@ $position_array[pos_]=$args['bag']	&	!@ помещаем объект в указанную позицию
!@ ----------------------------------------------- вывод на печать --------------------------------------------------
	:print
	if $run_array[arrpos('$id_array',$infop['last_id'])]<>'':
		last_id_['pos'] = arrpos('$id_array',$infop['last_id'])
	!@ динамический код при покупке, неважно на тело или в сумку
		if $infop['pay'] = 'true' and $strfind($run_array[last_id_['pos']],'!purc!') <> '':
			$temp_ = @run.dynamic.script('!purc!', $infop['last_id'], $infop['past_id'])
		end
	!@ динамический код при взятии, неважно на тело или в сумку
		if $infop['taked'] = 'true' and $strfind($run_array[last_id_['pos']],'!taked!') <> '':
			$temp_ = @run.dynamic.script('!taked!', $infop['last_id'], $infop['past_id'])
		end
	end
	@kill.var.olegus('$infop','pay','taked','last_id','past_id')
	@int.inventory
!@ ----------------------------------------------- вывод на печать --------------------------------------------------
--- take.obj ---------------------------------