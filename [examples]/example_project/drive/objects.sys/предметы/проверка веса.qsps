QSP-Game ! функция проверяет возможно ли добавление в рюкзак или инвентарь
$[0]	&	!	ай-ди предмета
$[1]	&	!	управляющая конструкция
# prv.obj.weight
local $obj_id_, $manage_ = $args[0], $args[1]
local $obj_body_, pos_, $obj_kind_, number_, weight_, $vector_, capacity_
local how_many_
$obj_body_ = @get.obj.id($obj_id_)	&	!@	получаем тело предмета
pos_ = arrpos('$id_array', $obj_id_)	&	!@	получаем позицию предмета в базе
$obj_kind_ = @em.tag.getCont($obj_body_)	&	!@	короткое имя предмета, ай-ди вида.

number_ = kolvo_array[pos_]	&	!@	количество предметов в позиции
weight_ = @em.tag.getNum($obj_body_, 'weight')	&	!@	вес одного предмета

if $strfind(@get.loc.id(),'(\[т:[\d]+\]_[\s\S]*|seller;|торговец;)')<>'':
	$vector_ = 'buy'
elseif $manage_ = 'inBag':
	$vector_ = 'inBag'
elseif $manage_ = 'inInv':
	$vector_ = 'inInv'
else:
	$vector_ = 'place'
end
!@ Вычисляем, сколько предметов такого веса может поместиться в рюкзаке
capacity_ = iif(weight_ <> 0, (property['hero.power.max'] - property['hero.power.all']) / weight_, 2123456789)
if number_>5 and capacity_>5:
	!@ Проверяем количество поднимаемых предметов. Если оно больше пяти, спрашиваем, сколько хочешь поднять.
	!@ Если количество поднимаемых предметов больше вместимости рюкзака, количество предметов, которые можно поднять ограничивается вместимостью
	!@ В противном случае это количество ограничено количеством поднимаемых предметов
	capacity_ = iif(number_ > capacity_, capacity_, number_)
	$how_many_ = $input(@b.d.t('hmayw', $vector_, capacity_))
	how_many_ = iif(isnum($how_many_)=-1, val($how_many_), 0)
else:
	!@ Если количество поднимаемых предметов 5 или меньше, и рюкзак может вместить лишь пять или меньше предметов, предметы поднимаются поштучно.
	how_many_ = 1
end
if how_many_ <> 0 and no how_many_*weight_>property['hero.power.max']-property['hero.power.all'] and no how_many_>capacity_:
	result = how_many_
else
	result = 0
end
--- prv.obj.weight ---------------------------------