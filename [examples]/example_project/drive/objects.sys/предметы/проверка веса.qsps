# prv.obj.weight
! функция проверяет возможно ли добавление в рюкзак или инвентарь
$args[0] = $args[0]	&	!	ай-ди предмета
$args[1] = $args[1]	&	!	управляющая конструкция
$args['obj'] = $func('get.obj.id',$args[0])	&	!	получаем тело предмета
args['pit']=arrpos('$id_array',$args[0])	&	!	получаем позицию предмета в базе
$args['short_name']=$func('em.tag.getCont',$args['obj'])	&	!	короткое имя предмета, ай-ди вида.

args['колво']=kolvo_array[args['pit']]	&	!	количество предметов в позиции
args['weight']=func('get.tag.num',$args['obj'],'weight')	&	!	вес одного предмета

if $strfind($func('get.loc.id'),'(\[т:[\d]+\]_[\s\S]*|seller;|торговец;)')!'':
	$args['поднять']='buy'
elseif $args[1]='inBag':
	$args['поднять']='inBag'
elseif $args[1]='inInv':
	$args['поднять']='inInv'
else
	$args['поднять']='place'
end
! Вычисляем, сколько предметов такого веса может поместиться в рюкзаке
if args['weight']!0: args['вместимость']=(property['hero.power.max']-property['hero.power.all'])/args['weight'] else args['вместимость']=2123456789
if args['колво']>5 and args['вместимость']>5:
! Проверяем количество поднимаемых предметов. Если оно больше пяти, спрашиваем, сколько хочешь поднять.
	! Если количество поднимаемых предметов больше вместимости рюкзака, количество предметов, которые можно поднять ограничивается вместимостью
	! В противном случае это количество ограничено количеством поднимаемых предметов
	if args['колво']>args['вместимость']: args['вместимость']=args['вместимость'] else args['вместимость']=args['колво']
	args['поднять_штук']=$input($func('b.d.t','hmayw',$args['поднять'],args['вместимость']))
	if isnum($args['поднять_штук'])=-1: args['поднять_штук']=val($args['поднять_штук']) else args['поднять_штук']=0
else
! Если количество поднимаемых предметов 5 или меньше, и рюкзак может вместить лишь пять или меньше предметов, предметы поднимаются поштучно.
	args['поднять_штук']=1
end
if args['поднять_штук']!0 and no args['поднять_штук']*args['weight']>property['hero.power.max']-property['hero.power.all'] and no args['поднять_штук']>args['вместимость']:
	result=args['поднять_штук']
else
	result=0
end
--- prv.obj.weight ---------------------------------