QSP-Game Функция проверяет, есть ли свободное "место на теле" для "предмета"
! если есть - заполняет массив $array_body_part[] айдишниками позиций и возвращает 'empty'
! если нет - возвращает "full"
$[0]	&	!	id объекта "предмет"
# prv.pos.inObj
local $obj_id_ = $args[0]
local $obj_body_, $pos_list_, $type_bp_
local $position_, count_
$obj_body_ = @get.obj.id($obj_id_)	&	!@	тело объекта "предмет"
$pos_list_ = @em.tag.getCont($obj_body_, 'pos')	&	!@	список позиций
if @em.tag.getCont($pos_list_,'t1') <> '': $type_bp_ = 't'	&	!@	тип позиции "рука" "перчатки"
if @em.tag.getCont($pos_list_,'c1') <> '': $type_bp_ = 'c'	&	!@	класс позиции "arm" "dosp"
if @em.tag.getCont($pos_list_,'p1') <> '': $type_bp_ = 'p'	&	!@	конкретная позиция "правая рука" "левая рука"
loop local i = 1 while len($pos_list_) > 0 step i += 1:
	$position_ = @em.tag.getCont($pos_list_,$type_bp_+$str(i))
	! получаем позицию из тега
	if $position_ <> '':
		count_+=1
		loop local pos_ = -1 while 1:
			pos_ = arrcomp('$object_array','[\s\S]*<body>[\s\S]*<<$position_>>[\s\S]*|[\s\S]*<<$position_>>[\s\S]*<body>[\s\S]*', pos_ + 1)
			if pos_ <> -1:
				if $include_array[pos_] <> '': jump 'continue'
				$array_body_part[arrsize('$array_body_part')] = $id_array[pos_]
			else:
				jump 'break'
			end
			:continue
		end
		:break
		$pos_list_ = $trim($replace($pos_list_,'<<$type_bp_>><<i>>:<<$position_>>:<<$type_bp_>><<i>>'))
	end
end
if arrsize('$array_body_part') < count_:
	$result = 'full'
	killvar '$array_body_part'
else
	$result = 'empty'
end
--- prv.pos.inObj ---------------------------------