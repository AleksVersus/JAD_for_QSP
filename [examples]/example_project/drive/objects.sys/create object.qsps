QSP-Game
! Подпрограмма создаёт новый объект в игре и присваивает ему айдишник
! Если используется в качестве функции, в результат возвращается ай-дишник
$[0]	&	!	 - тело объекта, исходник объекта
$[1]	&	!	 - положение, где находится объект
$[2]	&	!	 - Ключевое слово (короткое слово, идентификатор вида)
[3]		&	!	 - количество одинаковых предметов
[4]		&	!	 - заряд предмета
$[5]	&	!	 - динамический код, выполняемый во время действий с предметом.
[6]		&	!	 - позиция в которую нужно добавить предмет в базе !!!! ВНИМАНИЕ. Если в этой позиции в базе уже есть предмет, он будет затёрт.
# add.new.obj
local $obj_body_, $place_id_, $kind_ = $args[0], $args[1], $args[2]
local number_, charge_, $run_code_, pos_ = args[3], args[4], $args[5], args[6]
local pit_ = iif(pos_ <> 0, pos_, arrsize('$id_array'))
$object_array[pit_] = $obj_body_	&	!@	создаём объекту тело
!@ если объект является заголовком или инвентарём или респавнером, его ай-ди 
if $strfind($obj_body_,'<заголовок>') <> '' or $kind_ = 'INVENTORY' or $kind_='RESPAWNER':
	$id_array[pit_] = $kind_
elseif $strfind($obj_body_,'<link>') <> '':
	$id_array[pit_] = 'link.' + $TRIM($kind_)
else
	$id_array[pit_] = $kind_ + '.' + @em.str.random(8,'[inc][\d][\z][\я]')
end
if $strfind($obj_body_,'<заголовок>')<>'' and instr($obj_body_,'[respawn.stop]') = 0:
	$position_array[pit_] = 'RESPAWNER'
else
	$position_array[pit_] = $place_id_
end
kolvo_array[pit_]=number_
charge_array[pit_]=charge_
$run_array[pit_]=$run_code_
$result = $id_array[pit_]
--- add.new.obj ---------------------------------