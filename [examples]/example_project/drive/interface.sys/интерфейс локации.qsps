QSP-Game ! интерпретатор интерфейса локации
[0]	&	!	id локации
[1]	&	!	тип локации
# int.loc.interface
local $loc_id_, $loc_type_ = $args[0], $args[1]
local $rn_ = @em.RN
local $color_design_
if $SQUARE['SQUARE.loc.type'] = 'место':
!@ Тип локации определён, как нормальная локация места, значит вызываем структуру для локации места:
	if $light_array[$loc_id_] = 'темно': $color_design_ = 'темно'
	$to_main_screen_ = @interface.place($loc_id_)
	if $GAME_INTERFACE['show.stat']='show': $to_stat_screen_=$SQUARE['SQUARE.stat']
	if $GAME_INTERFACE['show.acts']='show':
		$to_acts_screen_+=$rn_+$SQUARE['SQUARE.loc.curacts']+$rn_
		$to_acts_screen_+=$SQUARE['SQUARE.loc.acts.inActs']
		$to_acts_screen_+=$SQUARE['SQUARE.location.obj.inActs']
	end
elseif $SQUARE['SQUARE.loc.type'] = 'хранилище':
	$to_main_screen_=@interface.chest($loc_id_)
	if $GAME_INTERFACE['show.stat']='show': $to_stat_screen_=$SQUARE['SQUARE.stat']
	if $GAME_INTERFACE['show.acts']='show':
		$to_acts_screen_=''
		!@ Внимание!!! При любом режиме хранилище показывается в виде с отключенными действиями.
	end
elseif $SQUARE['SQUARE.loc.type'] = 'кузница':
	$to_main_screen_=@interface.smith($loc_id_)
else
	$to_main_screen_+=$SQUARE['SQUARE.loc.maintxt']			&	!@	базовое описание выводится в первую очередь
	$to_main_screen_+=$SQUARE['SQUARE.loc.hallow']			&	!@	следующим по порядку идёт приветствие
	$to_main_screen_+=$SQUARE['SQUARE.loc.fromSource']		&	!@	описание из исходника
	$to_main_screen_+=$SQUARE['SQUARE.loc.plustext']		&	!@	добавочный текст
	$to_main_screen_+=$SQUARE['SQUARE.loc.dvar']
	if $GAME_INTERFACE['show.stat']='show': $to_stat_screen_=$SQUARE['SQUARE.stat']
	if $GAME_INTERFACE['show.acts']='show':
		$to_acts_screen_+=$rn_+$SQUARE['SQUARE.loc.curacts']+$rn_
		$to_acts_screen_+=$SQUARE['SQUARE.loc.acts.inActs']
	end
end
cls
if $to_main_screen_<>'': *pl @int.din.text($to_main_screen_)
if $to_stat_screen_<>'': pl @int.din.text($to_stat_screen_)
if $to_acts_screen_<>'': dynamic $to_acts_screen_
@setScreen($SQUARE['SQUARE.loc.type'],$color_design_)
@int.inventory()
--- int.loc.interface ---------------------------------