# interface.chest
! таблица в основное описание локации места. Понадобятся значения следующих переменных:
! "общие переменные режимов
	$SQUARE['SQUARE.loc.type']
	$GAME_INTERFACE['show.stat']
	$GAME_INTERFACE['show.acts']
	$GAME_INTERFACE['maintxt']
! общие переменные интерфейса
!	$SQUARE['SQUARE.loc.maintxt']
!	$SQUARE['SQUARE.loc.fromSource']
!	$SQUARE['SQUARE.loc.hallow']
!	$SQUARE['SQUARE.loc.plustext']
!	$SQUARE['SQUARE.loc.dvar']
!	$SQUARE['SQUARE.loc.acts.inHREF']
!	$SQUARE['SQUARE.head']
!	$SQUARE['SQUARE.stat']
! переменные для хранилищ/кузниц/алтарей/проч...
!	$SQUARE['SQUARE.chest.closeAct']
! переменные для хранилища
!	$SQUARE['SQUARE.chest.obj.inHREF']
!"
$args[0] = $args[0]	&	!	id локации
! формируем заголовок:
$args['head.text']+=$SQUARE['SQUARE.head']
! формируем дополнительное описание:
$args['stat.text']+=$SQUARE['SQUARE.stat']
! формируем действия:
$args['acts.text']+=$SQUARE['SQUARE.loc.acts.inHREF']		&	!	действия из массива $avar
! формируем основное описание:
$args['main.text']+=$SQUARE['SQUARE.loc.maintxt']			&	!	базовое описание выводится в первую очередь
$args['main.text']+=$SQUARE['SQUARE.loc.hallow']			&	!	следующим по порядку идёт приветствие
$args['main.text']+=$SQUARE['SQUARE.loc.fromSource']		&	!	описание из исходника
$args['main.text']+=$SQUARE['SQUARE.loc.plustext']			&	!	добавочный текст
$args['main.text']+=$SQUARE['SQUARE.loc.dvar']				&	!	текст в накидку
if $args['acts.text']<>'' and $args['acts.text']<>'': $args['main.text']+='<br>'
$args['main.text']+=$args['acts.text']						&	!	действия на локации в основном описании

	! начало таблицы
	$args['interface']+='<table width=100% valign=middle cellpadding=8 border=3>'
		! строка заголовка. Одинаковая для всех режимов:
		$args['interface']+="<tr><td align=center valign=middle>"+$args['head.text']+"</td></tr>"
		! основной текст вертикальный:
		if $GAME_INTERFACE['maintxt']='vertical':
			! если режим статов main under отдельной строкой пишется основное описание
			if $GAME_INTERFACE['show.stat']="main under" and $args['main.text']<>'':
				! начало строки и ячейки
				$args['interface']+="<tr><td align=left valign=top><div align=justify>"
				! содержимое основного описания:
					$args['interface']+=$args['main.text']
				! конец строки и ячейки
				$args['interface']+="</div></td></tr>"
			end
			! если режим статов main under отдельной строкой пишется дополнительное описание
			if $GAME_INTERFACE['show.stat']="main under" and $args['stat.text']<>'':
				! начало строки и ячейки
				$args['interface']+="<tr><td align=left valign=top><div align=justify>"
				! содержимое основного описания:
					$args['interface']+=$args['stat.text']
				! конец строки и ячейки
				$args['interface']+="</div></td></tr>"
			end
			! начало строки/ячейки/таблицы со списком предметов/мэйном/статом
			$args['interface']+="<tr><td align=center valign=top><table width=100% cellpadding=5 border=2>"
				! ячейка/таблица мэйн/стат начало
				$args['main|stat']+='<td width=35%><table width=100% border=1>'
					! строка/ячейка - основное опписание начало
					$args['main|stat']+='<tr><td>'
						if $args['main.text']<>'': $args['main|stat']+=$args['main.text'] else $args['main|stat']+='&nbsp;'
					! строка/ячейка - основное опписание конец
					$args['main|stat']+='</td></tr>'
					if $args['stat.text']<>'':
					! строка ячейка доп описания
						$args['main|stat']+='<tr><td>'
						$args['main|stat']+=$args['stat.text']
						$args['main|stat']+='</td></tr>'
					end
				! ячейка/таблица мэйн/стат конец
				$args['main|stat']+='</table></td>'
				if $GAME_INTERFACE['show.stat']="main left": $args['interface']+=$args['main|stat']
				! ячейка с предметами начало
				$args['interface']+='<td align=left valign=bottom>'
					$args['interface']+=$SQUARE['SQUARE.chest.obj.inHREF']
					$args['interface']+='<br>'+$SQUARE['SQUARE.chest.closeAct']
				!ячейка с предметами конец
				$args['interface']+='</td>'
				if $GAME_INTERFACE['show.stat']="main right": $args['interface']+=$args['main|stat']
			! конец строки/ячецки/таблицы со списокм/мэйном/статом
			$args['interface']+='</table></td></tr>'
		elseif $GAME_INTERFACE['maintxt']='horizontal':
			! начало строки/ячейки с мэйном
			$args['interface']+='<tr><td width=100%>'
				$args['interface']+=$args['main.text']
			! конец строки/ячейки с мэйном
			$args['interface']+='</td></tr>'
			! начало строки/ячейки таблицы со сброшенными предметами и статом
			$args['interface']+='<tr><td><table width=100% border=2 cellspacing=5><tr>'
				! ячейка стат
				if $args['stat.text']<>'':
				! строка ячейка доп описания
					$args['main|stat']+='<td width=35% align=left valign=top>'
					$args['main|stat']+=$args['stat.text']
					$args['main|stat']+='</td>'
				end
				if $GAME_INTERFACE['show.stat']="main left" and $args['main|stat']<>'': $args['interface']+=$args['main|stat']
				! ячейка сброшенных предметов
				$args['interface']+='<td valign=top align=left>'
					$args['interface']+=$SQUARE['SQUARE.chest.closeAct']+'<br>'
					$args['interface']+=$SQUARE['SQUARE.chest.obj.inHREF']
				$args['interface']+='</td>'
				if $GAME_INTERFACE['show.stat']="main right" and $args['main|stat']<>'': $args['interface']+=$args['main|stat']
			! конец строки/ячейки таблицы со сброшенными предметами и статом
			$args['interface']+='</tr></table></td></tr>'
			! строка ячейка статов при main under
			if $args['stat.text']<>'' and $GAME_INTERFACE['show.stat']="main under":
				$args['interface']+='<tr><td align=left valign=top>'
					$args['interface']+=$args['stat.text']
				$args['interface']+='</td></tr>'
			end
		end
	! конец таблицы
	$args['interface']+="</table>"
	
$result = $args['interface']
--- interface.chest ---------------------------------