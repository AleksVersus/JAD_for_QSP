# interface.place
! таблица в основное описание локации места. Понадобятся значения следующих переменных:
! "общие переменные режимов
	$SQUARE['SQUARE.loc.type']
	$GAME_INTERFACE['show.stat']
	$GAME_INTERFACE['show.acts']
	$GAME_INTERFACE['maintxt']
! общие переменные интерфейса
!	$SQUARE['SQUARE.loc.maintxt']
!	$SQUARE['SQUARE.loc.fromSource']
!	$SQUARE['SQUARE.loc.hallow']
!	$SQUARE['SQUARE.loc.plustext']
!	$SQUARE['SQUARE.loc.dvar']
!	$SQUARE['SQUARE.loc.acts.inHREF']
!	$SQUARE['SQUARE.head']
!	$SQUARE['SQUARE.stat']
! переменные для локаций-мест:
!	$SQUARE['SQUARE.location.obj.inHREF']
!	$SQUARE['SQUARE.location.indark.chest']
!"
$args[0] = $args[0]	&	!	id локации
! формируем заголовок:
$args['head.text']+=$SQUARE['SQUARE.head']
! формируем дополнительное описание:
$args['stat.text']+=$SQUARE['SQUARE.stat']
! формируем действия:
$args['acts.text']+=$SQUARE['SQUARE.loc.acts.inHREF']		&	!	действия из массива $avar идут в первую очередь
$args['acts.text']+=$SQUARE['SQUARE.location.obj.inHREF']	&	!	действия <obj> <goto> <act>
! формируем основное описание:
if $light_array[$args[0]]='темно':
	$args['main.text']+=$func('b.d.t','dark')
	$args['main.text']+=$SQUARE['SQUARE.location.indark.chest']
else
	$args['main.text']+=$SQUARE['SQUARE.loc.maintxt']			&	!	базовое описание выводится в первую очередь
	$args['main.text']+=$SQUARE['SQUARE.loc.hallow']			&	!	следующим по порядку идёт приветствие
	$args['main.text']+=$SQUARE['SQUARE.loc.fromSource']		&	!	описание из исходника
	$args['main.text']+=$SQUARE['SQUARE.loc.plustext']			&	!	добавочный текст
	$args['main.text']+=$SQUARE['SQUARE.loc.dvar']				&	!	текст в накидку
end
	! начало таблицы
	$args['interface']+="<table width=100% valign=middle cellpadding=0 border=1>"
		! ячейка заголовка. Одинаковая для всех режимов.
		$args['interface']+="<tr><td align=center valign=middle>"+$args['head.text']+"</td></tr>"
		! ячейка/таблица описания/статов/действий
		$args['interface']+="<tr><td><table width=100% cellpadding=12 border=2>"
		if $GAME_INTERFACE['maintxt']='vertical':
		! вертикальная разметка. Дополнительное описание располагается рядом с основным и действиями, т.е. одной строкой
			! начало общей строки
			$args['interface']+='<tr>'
			if $GAME_INTERFACE['show.stat']='main left':
			! если статы выводятся слева, создеём отдельную ячейку
				$args['interface']+='<td align=center valign=bottom width=38%><div align=justify>'
				if $args['stat.text']<>'': $args['interface']+=$args['stat.text'] else $args['interface']+='&nbsp;'
				$args['interface']+='</div></td>'
			end
			! создаём ячейку для основного описания и действий и ещё таблицу:
			$args['interface']+='<td align=left valign=top><table width=100% cellpadding=5 border=1>'
				! строка и ячейка с основным описанием
				$args['interface']+="<tr><td width=100% valign=top align=center height=400><div align=justify>"
					if $args['main.text']<>'': $args['interface']+=$args['main.text'] else $args['interface']+='&nbsp;'
				! закрываем строку и ячейку с основным описанием
				$args['interface']+="</div></td></tr>"
				! В режиме main under дополнительное описание располагается сразу под основным
				if $GAME_INTERFACE['show.stat']='main under' and $args['stat.text']<>'':
				! строка/ячейка дополнительного описания не создаётся, если не нужна
					$args['interface']+="<tr><td width=100% valign=top align=center height=400><div align=justify>"
						$args['interface']+=$args['stat.text']
					$args['interface']+="</div></td></tr>"
				end
				! действия добавляются в таблицу только при show.acts=hide
				if $GAME_INTERFACE['show.acts']='hide' and $args['acts.text']<>'':
				! строка/ячейка действий не создаётся, если их нет
					$args['interface']+="<tr><td width=100% valign=top align=left bgcolor=#"+$GAME_INTERFACE['acts.color']+">"
						$args['interface']+='<font size=1>'+$func('b.d.t','word.acts')+':</font><br>'
						$args['interface']+=$args['acts.text']
					$args['interface']+="</td></tr>"
				end
			! закрываем таблицу и ячейку
			$args['interface']+='</table></td>'
			if $GAME_INTERFACE['show.stat']='main right':
			! если статы выводятся справа, создаём отдельную ячейку
				$args['interface']+='<td align=center valign=bottom width=38%><div align=justify>'
				if $args['stat.text']<>'': $args['interface']+=$args['stat.text'] else $args['interface']+='&nbsp;'
				$args['interface']+='</div></td>'
			end
			! конец общей строки
			$args['interface']+='</tr>'
		elseif $GAME_INTERFACE['maintxt']='horizontal':
		! горизонатльная разметка. дополнительное описание и действия располагаются под основным описанием
			! отдельной строкой и ячейкой размещаем основное описание
			$args['interface']+='<tr><td width=100% height="400 px."><div align=justify>'
				if $args['main.text']<>'': $args['interface']+=$args['main.text'] else $args['interface']+='&nbsp;'
			$args['interface']+='</div></td></tr>'
			! строка/ячейка/таблица с действиями и статами. Выводится только если хотя бы одно из окон скрыто
			if $GAME_INTERFACE['show.acts']='hide' or $GAME_INTERFACE['show.stat']<>'show':
				$args['interface']+='<tr><td width=100%><table width=100% border=1>'
					! общая строка
					$args['interface']+='<tr>'
						if $GAME_INTERFACE['show.stat']='main left' or $GAME_INTERFACE['show.stat']='main right':
							if $GAME_INTERFACE['show.acts']='hide': args["w"]=35 else args["w"]=100
							$args['stat.cell']='<td width=<<args["w"]>>% align=center valign=top><div align=justify>'
							if $args['stat.text']<>'': $args['stat.cell']+=$args['stat.text'] else $args['stat.cell']+='&nbsp;'
							$args['stat.cell']+='</div></td>'
						end
						if $GAME_INTERFACE['show.stat']='main left':	$args['interface']+=$args['stat.cell']
						if $GAME_INTERFACE['show.acts']='hide':
							if $args['acts.text']<>'':
								$args['interface']+="<td valign=top align=left bgcolor=#"+$GAME_INTERFACE['acts.color']+">"
									$args['interface']+='<font size=1>'+$func('b.d.t','word.acts')+':</font><br>'
									$args['interface']+=$args['acts.text']
								$args['interface']+="</td>"
							else
								$args['interface']+='<td>&nbsp;</td>'
							end
						end
						if $GAME_INTERFACE['show.stat']='main right':	$args['interface']+=$args['stat.cell']
					$args['interface']+='</tr>'
					! если show.stat = main under размещаем отдельной строкой
					if $GAME_INTERFACE['show.stat']='main under' and $args['stat.text']<>'':
						$args['interface']+="<tr><td width=100% valign=top align=center height=400><div align=justify>"
							$args['interface']+=$args['stat.text']
						$args['interface']+="</div></td></tr>"
					end
				$args['interface']+='</table></td></tr>'
			end
		end
		! закрываем ячейку/таблицу описания/статов/действий
		$args['interface']+='</table></td></tr>'
	! конец таблицы
	$args['interface']+="</table>"
$result = $args['interface']
--- interface.place ---------------------------------