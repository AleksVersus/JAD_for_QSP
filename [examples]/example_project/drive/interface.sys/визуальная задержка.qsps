QSP-Game
! функция обеспечивает выведение набора символов для визуальной задержки
$[0]=':'		&	!	набор символов
[1]=100			&	!	время задержки между символами
[2]=[1]*10 		&	!	общее время продолжительности вывода строки в милисекундах
$[3]='ff0000'	&	!	начальный цвет
$[4]='0000ff'	&	!	конечный цвет
[5]				&	!	размер шрифта
[6] 			&	!	режим 0 - вывод в окне основного описания и дополнительного, 1 - основного, 2 дополнительного
# waiting.string
!@ извлечение аргументов
local $symbols_, delay_, time_len_ = $args[0], args[1], args[2]
local $start_col_, $end_col_, fsize_, mode_ = $args[3], $args[4], args[5], args[6]
!@ цветовые составляющие
local red_, green_, blue_
red_['start'] = @em.hex.inDec($mid($start_col_,1,2))
green_['start']=func('em.hex.inDec',$mid($start_col_,3,2))
blue_['start']=func('em.hex.inDec',$mid($start_col_,5,2))
red_['end']=func('em.hex.inDec',$mid($end_col_,1,2))
green_['end']=func('em.hex.inDec',$mid($end_col_,3,2))
blue_['end']=func('em.hex.inDec',$mid($end_col_,5,2))
!@ пропорции
local i =time_len_ / delay_	& !@ interval_
red_['incr'] = (red_['start']-red_['end'])/i	& !@ increment
green_['incr'] = (green_['start']-green_['end'])/i
blue_['incr'] = (blue_['start']-blue_['end'])/i
if red_['start'] > red_['end'] and red_['incr'] = 0: red_['incr'] = 1
if green_['start'] > green_['end'] and green_['incr'] = 0: green_['incr'] = 1
if blue_['start'] > blue_['end'] and blue_['incr'] = 0: blue_['incr'] = 1
if red_['start'] < red_['end'] and red_['incr'] = 0: red_['incr'] = -1
if green_['start'] < green_['end'] and green_['incr'] = 0: green_['incr'] = -1
if blue_['start'] < blue_['end'] and blue_['incr'] = 0: blue_['incr'] = -1

!@ цикл, обеспечивающий визуальную задержку
local $output_
loop local u = 1 while i > -1 step i -=1 & u +=1:
	if $mid($symbols_, u, 1)='': u=1
	$output_ = '<font color=#'+@em.dec.inCol(red_['start'])+@em.dec.inCol(green_['start'])+@em.dec.inCol(blue_['start'])+' size=+'+$str(fsize_)+'><b><<$mid($symbols_,u,1)>></b></font>'
	if mode_=0 or mode_=1: *p $output_
	if mode_=0 or mode_=2: p $output_

	red_['start'] -= red_['incr']
	green_['start'] -= green_['incr']
	blue_['start'] -= blue_['incr']
	if red_['start'] < 0: red_['start'] = 0
	if green_['start'] < 0: green_['start' ] = 0
	if blue_['start'] < 0: blue_['start'] = 0
	if red_['start'] > 255: red_['start'] = 255
	if green_['start'] > 255: green_['start'] = 255
	if blue_['start'] > 255: blue_['start'] = 255
	
	wait delay_
end
--- waiting.string ---------------------------------