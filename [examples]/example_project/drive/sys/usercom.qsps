# inputText
!@ читкоды ! ОТЛАДЧИК!
$args[0] = $args[0]
local $user_text_, $use_, $res_, moment_1_, moment_2_
if $args[0] <> '':
	$user_text_ = $args[0]
	$use_ = 'func'
else
	$user_text_ = $user_text
	$use_ = 'print'
end

!@ ------------------------------------- парсер строки ввода ----------------------------------
	local $input_text_word_
	loop local $string_ = $user_text_ while len($string_)>0:
		$input_text_word_[] = $TRIM($strfind($string_, '\s*\S+\s*'))
		$string_ = $TRIM($replace($string_, $strfind($string_, '\s*\S+\s*')))
	end
!@ ------------------------------------- парсер строки ввода ----------------------------------

if $input_text_word_[0] = 'run':
	$res_ = $dyneval($mid($user_text_, 5))
	jump 'end_keep'
end
if $input_text_word_[0] = 'arr':
	!@ выводит содержимое массивов
	$res_=@em.arr.print($input_text_word_[1])
	jump 'end_keep'
end
if $input_text_word_[0] = 'obj':
	!@ выводит содержимое предмета по числу или ай-ди
	if isnum($input_text_word_[1])=-1:
		$res_ += @inputText.obj(val($input_text_word_[1]))
	elseif $input_text_word_[1]!'':
		$res_ += @inputText.obj(arrpos('$id_array',$input_text_word_[1]))
	end
end
if $input_text_word_[0] = 'all':
	if $input_text_word_[1] = 'objects':
		if $input_text_word_[2] = 'table':
			moment_1_ = MSECSCOUNT
			$res_ = '<table width=100% cellpadding=0 cellspacing=2 border=1><tr><td>№П.П.</td><td>ID</td><td>Тело объекта</td><td>расположение</td><td>Дочерний:</td><td>кол-во</td><td>заряд</td></tr>'
			loop local i = 0 while arrsize('$id_array') > i step i+=1:
				$res_ += '<tr><td>[<<i>>]</td><td><<$id_array[i]>></td><td>'+$replace($replace($object_array[i],'<','&lt;'),'>','&gt;')+'</td><td>{<<$position_array[i]>>}</td><td>^<<$include_array[i]>>^</td><td><<kolvo_array[i]>></td><td><<charge_array[i]>></td></tr>'
			end
			$res_+='<tr><td>№П.П.</td><td>ID</td><td>Тело объекта</td><td>расположение</td><td>Дочерний:</td><td>кол-во</td><td>заряд</td></tr></table>'
		elseif $input_text_word_[2]='dialog':
			$res_ = @em.fewArrs.print('$easy_dialog_base_ID','$easy_dialog_base_SC','$easy_dialog_base_ST','$easy_dialog_base_PS','easy_dialog_base_KOL')
			jump 'end_keep'
		elseif $input_text_word_[2] = '':
			loop local i = 0 while arrsize('$id_array') > i step i += 1:
				$res_ += '<br>[<<i>>] <<$id_array[i]>> <<$object_array[i]>> {<<$position_array[i]>>}'
				$res_ += ' количество:<<kolvo_array[i]>> заряд:<<charge_array[i]>>'
			end
		end
	end
end
if $input_text_word_[0] = 'daughters':
	if isnum($input_text_word_[1]) = -1:
		$input_text_word_[1] = $id_array[val($input_text_word_[1])]
	end
	local $temp_id_input_text_
	if @get.daughter.obj($input_text_word_[1], '', '$temp_id_input_text_') = 'true':
		$res_ += '<br><b>Объект:</b> ' + @inputText.obj(arrpos('$id_array',$input_text_word_[1]))
		$res_ += '<br><b>Список id дочерних объектов:</b> '
		loop local i = 0 while arrsize('$temp_id_input_text_')>0 step i += 1:
			$res_+='<br>['+$str(arrpos('$id_array',$temp_id_input_text_[0]))+']'+$temp_id_input_text_[0]
			killvar '$temp_id_input_text_',0
		end
	else:
		$res_+='<br><b>Дочерних объектов нет.</b>'
	end
end
if $input_text_word_[0] = 'goto':
	local $location_ = $trim($replace($user_text_ ,'goto '))
	if $strfind($location_,'(\[м:[\d]+\]_[\s\S]+|место;|place;)')<>'' and loc($location_):
		$lvar['локация.дополнительные тексты'] += "<br><br><b>Ты премести(лся/лась) на локацию '<<$location_>>'</b><br>"
		goto $location_
	elseif $strfind($location_,'(\[с:[\d]+\]_[\s\S]+|сундук;|chest;)') <> '' or $strfind($location_,'(\[т:[\d]+\]_[\s\S]+|торговец;|seller;)') <> '':
		$lvar['локация.дополнительные тексты'] += "<br><br><b>Ты премести(лся/лась) на локацию '<<$location_>>'</b><br>"
		goto '[chest]', $location_, '', @get.loc.id()
	else
		$res_ = 'Сообщение отладчика: локация с id "<<$location_>>" не существует, или является служебной.'
	end
end
if $user_text_ = 'main html':
	$res_ = $replace($maintxt, '<', '&lt;')
	$res_ = $replace($res_, '>', '&gt;')
end
if $input_text_word_[0] = 'logs':
	if $input_text_word_[1] <> 'error':
		$res_ += 'Лог дополнительного описания (последние сто записей):<br>'
		$res_ += @em.arr.print('$log_print')+'<br>'
	end
	$res_ += 'Лог движка:<br>'
	loop local i = 0 while i<arrsize('$error_log') step i += 1: $res_ += $error_log[i]+'<br>'
	$res_ += '<br>'
	$res_ += 'Лог ошибок движка:<br>'
	$res_ += @em.arr.print('$log_error','')+'<br>'
end
:end_keep
moment_2_ = MSECSCOUNT
if	$use_='func': $result = '<<$res_>><br>Вывод таблицы объектов занял мсек: <<moment_2_-moment_1_>>'
if	$use_='print' and $input_text_word_[0] <> 'run': *pl $res_
--- inputText ---------------------------------

# inputText.obj
args[0] = args[0]
$result  += @em.RN + 'Предмет номер <<args[0]>>:'
$result  +='<font color=#888888>' + @em.RN + 'ID[<<args[0]>>]	=	'+$id_array[args[0]]
$result  +='</font>' + @em.RN + 'OBJ[<<args[0]>>]	=	'+$replace($object_array[args[0]],'<','&lt;')
$result  +='<font color=#888888>' + @em.RN + 'POS[<<args[0]>>]	=	'+$position_array[args[0]]
$result  +='</font>' + @em.RN + 'количество: <<kolvo_array[args[0]]>>, заряд: <<charge_array[args[0]]>>'
$result  +='<font color=#888888>' + @em.RN + 'респавнится в <<$respawn_array[args[0]]>>'
$result += '</font>' + @em.RN + 'RUN:' + @em.RN + $replace($replace($run_array[args[0]], '<', '&lt;'), '>', '&gt;')
--- inputText.obj ---------------------------------