
# onObjSel
local $temp_, $loc_specialized_, $color_, $name_
local $menu_OOS, $tempOOS_id, $OOS
$OOS['oid']=@em.tag.getCont($selobj,'oid')	&	!@ получаем ай-ди выделенного объекта. Айди хранится в виде html тега <oid: :oid>
OOS['pos']=arrpos('$id_array',$OOS['oid'])	&	!@ получаем его позицию в базе
$OOS['obj']=$object_array[OOS['pos']]	&	!@ получаем его тело
$OOS['np']=@em.tag.getCont($OOS['obj'],'np')	&	!@ набор признаков
$OOS['short_word']=@em.tag.getCont($OOS['obj'])	&	!@ ключевое слово, ай-ди вида
!@ В зависимости от класса, производим операции
if instr($OOS['obj'],'[nocount]')!0:
!@ неисчислимый предмет, помещённый в панель инвентаря!!!@ 	!заменяем данные на данные кошелька.
	if @get.daughter.obj('INVENTORY','<money>','$tempOOS_id') = 'true': $OOS['oid'] = $tempOOS_id[]
	killvar '$tempOOS_id'
	OOS['pos']=arrpos('$id_array',$OOS['oid'])	&	!@ получаем его позицию в базе
	$OOS['obj']=$object_array[OOS['pos']]
	!@ отправляемся к выводу содержимого кошелька. {Заменить на подпрограмму.}
	jump 'money_print'
elseif instr($OOS['obj'],'<body>')!0:
!@ если объект является "частью тела" в дополнительное окно описания выводятся все имеющиеся "предметы",
!@ которые можно надеть на указанную часть тела.
	!@ Вычисляем часть тела:
	$OOS['pos_list']=@em.tag.getCont($OOS['obj'])
	!@ 1	производим поиск дочерних объектов в инвентаре по классу <bag> и <obj>
	killvar '$tempOOS_id'
	$temp_ = @get.daughter.obj.all('INVENTORY','(<bag>|<obj>)+','$tempOOS_id')
	!@ Теперь у нас есть все предметы, какие только возможны. Из них мы отсеиваем все, которые невозможно одеть на нашу часть тела.
		local $array_body_part_, $array_body_part_pos_
		loop local p_ = arrsize('$tempOOS_id')-1 while p_ > -1 step p_ -= 1:
			if @prv.pos.inObj($tempOOS_id[p_]) = 'empty':
				loop local t = 0 while t < arrsize('$array_body_part_') step t += 1:
					$array_body_part_pos_[t] = @em.tag.getCont($object_array[arrpos('$id_array', $array_body_part_[t])])
				end
				if arrcomp('$array_body_part_pos_','[\s\S]*'+$OOS['pos_list']+'[\s\S]*') <> -1:
					$print['obj_in_pos'] += @gen.ahref.actObj($tempOOS_id[p_]) + '<br>'
				end
			end
		end
	killvar '$tempOOS_id'
	!@ 1	список дочерних объектов уничтожается
	if $print['obj_in_pos']<>'':
		$print['obj_in_pos'] = @b.w.s('obj.inPos')+'<br>'+$print['obj_in_pos']
	else:
		$print['obj_in_pos'] = @b.w.s('none.obj.inPos')
	end
elseif instr($OOS['obj'],'<obj>') <> 0:
!@ если объект является "предметом"
	$menu_OOS[arrsize('$menu_OOS')] = @b.d.t('menu.obj.property')+':menu.obj.property'
	$menu_OOS[arrsize('$menu_OOS')] = '-:-'
	!@ действия над типовыми объектами:
	if instr($OOS['np'],'[книга]') <> 0:
		$menu_OOS[arrsize('$menu_OOS')] = @b.d.t('menu.obj.read')+':menu.obj.read'
	end
	if instr($OOS['np'],'[огнестрельное]') <> 0:
		local $id_arm_temp_
		$temp_ = @get.daughter.obj($OOS['oid'],'','$id_arm_temp_')
		if $temp_ = 'true': $menu_OOS[arrsize('$menu_OOS')] = @b.d.t('menu.obj.disCharge')+':menu.obj.disCharge'
	end
	if instr($OOS['np'],'[еда]') <> 0: $menu_OOS[arrsize('$menu_OOS')] = @b.d.t('menu.obj.eat')+':menu.obj.eat'
	if instr($OOS['np'],'[свиток]') <> 0:
		$menu_OOS[arrsize('$menu_OOS')] = @b.d.t('menu.obj.spell.read')+':menu.obj.spell.read'
		$menu_OOS[arrsize('$menu_OOS')] = @b.d.t('menu.obj.spell.teach')+':menu.obj.spell.teach'
	end
	if $strfind(@get.loc.id(),'(\[с:[\d]+\]_[\S\s]+|сундук;|chest;)')<>'':
		$temp_ = @get.obj.id($cvar['loc_id'+$cvar['chest']])
		$loc_specialized_=@em.tag.getCont($temp_, 'spec')
		if instr($loc_specialized_,'[smithing]') <> 0 and instr($object_array[arrpos('$id_array',$position_array[OOS['pos']])],'<body>')=0:
			$menu_OOS[arrsize('$menu_OOS')] = @b.d.t('menu.obj.unmount')+':menu.obj.unmount'
		end
		if instr($loc_specialized_,'[altar]') <> 0 and instr($OOS['np'],'[посох]') <> 0:
		!@ специальные качества локации - алтарь, и предмет заряжаемый
			!@ получаем дочерние объекты алтаря
			local $id_obj_onaltar_
			$temp_ = @get.daughter.obj($cvar['loc_id'+$cvar['chest']],'','$id_obj_onaltar_','!<onALTAR>[\s\S]*!</onALTAR>')
			if $temp_ <> 'true': $menu_OOS[arrsize('$menu_OOS')] = @b.d.t('menu.obj.put.onAltar')+':menu.obj.put.onAltar'
		end
	end
	if instr($OOS['np'],'[патроны]')!0: $menu_OOS[arrsize('$menu_OOS')]=@b.d.t('menu.obj.inCharge')+':menu.obj.inCharge'
	!@ действия над типовыми объектами
	gs 'int.menu.obj',$OOS['obj']	&	!@ вытаскивает  меню из тегов menu::menu
	$OOS['bdy']=@prv.str.inObj($position_array[OOS['pos']],'<body>')
	$OOS['inv']=@prv.str.inObj($position_array[OOS['pos']],'<inventory>')
	$OOS['bag']=@prv.str.inObj($position_array[OOS['pos']],'<bag>')
	if $OOS['bdy']='true' or $OOS['inv']='true':
	!@ если предмет находится в позиции части тела или инвентаре, действие меню "убрать в сумку"
		killvar '$tempOOS_id'
		if @get.daughter.obj('INVENTORY','<bag>','$tempOOS_id')='true': $menu_OOS[arrsize('$menu_OOS')]=@b.d.t('menu.in.bag')+':menu.in.bag'
		killvar '$tempOOS_id'
	end
	if ($OOS['bag']='true' or $OOS['inv']='true') and (instr($OOS['obj'],'[оружие]')!0 or instr($OOS['obj'],'[доспех]')!0 or instr($OOS['obj'],'[стрелы]')!0 or instr($OOS['obj'],'[болты]')!0):
	!@ если предмет находится непосредственно в инвентаре, или сумке, действие "использовать" (одеть)
		$menu_OOS[arrsize('$menu_OOS')]=@b.d.t('menu.in.body')+':menu.in.body'
	end
	if $OOS['bag']='true' or $OOS['bdy']='true':
	!@ если предмет находится непосредственно на теле, или сумке, действие "убрать в инвентарь"
		$menu_OOS[arrsize('$menu_OOS')]=@b.d.t('menu.in.inventory')+':menu.in.inventory'
	end
	OOS['heading_n']=arrpos('$id_array',@get.loc.id())
	if $strfind($object_array[OOS['heading_n']],'\[put.stop\]')='':
		$menu_OOS[arrsize('$menu_OOS')]='-:-'
		if $strfind(@get.loc.id(),'(\[т:[\d]+\]_[\S\s]+|торговец;|seller;)')!'':
			temp_['1'] = @em.tag.getNum($OOS['obj'],"stoim")
			$temp_['2'] = @em.indiv(@obj.sale.cost(temp_['1'], @get.loc.id()),100,1)
			$menu_OOS[arrsize('$menu_OOS')]=@b.d.t('menu.obj.put','sale',$temp_['2'])+':menu.obj.put'
		elseif $strfind(@get.loc.id(),'(\[м:[\d]+\]_[\S\s]+|place;|место;)')!'':
			$menu_OOS[arrsize('$menu_OOS')]=@b.d.t('menu.obj.put','put')+':menu.obj.put'
		elseif instr($loc_specialized_,'[smithing]')!0:
			$menu_OOS[arrsize('$menu_OOS')]=@b.d.t('menu.obj.put','smithing')+':menu.obj.put'
		elseif instr($loc_specialized_,'[altar]')!0:
			$menu_OOS[arrsize('$menu_OOS')]=@b.d.t('menu.obj.put','altar')+':menu.obj.put'
		else
			$menu_OOS[arrsize('$menu_OOS')]=@b.d.t('menu.obj.put','chest')+':menu.obj.put'
		end
	end
elseif instr($OOS['obj'],'<money>')!0:
!@ если выделенным объектом является кошелёк
	:money_print
	$color_=@em.tag.getNum($OOS['obj'],'color')
	$name_=@em.tag.getCont($OOS['obj'],'name')
	$name_=@get.word.padez($name_,'И')
	$print['money']+='<font color=#'+$color_+' face="CyrillicOld" size=+1><b>'+$name_+':</b></font><br>'
	$temp_=@get.daughter.obj($OOS['oid'],'','$tempOOS_id')
	if $temp_='true':
		!@ находим все объекты в кошельке и выводим информацию:
		:for2
		if arrsize('$tempOOS_id')>0:
			OOS['pos1']=arrpos('$id_array',$tempOOS_id[0])
			$temp_ = @em.tag.getCont($object_array[OOS['pos1']],'name')
			$temp_ = @get.word.padez($temp_,'И')
			$print['money']+='<font color=#'+@em.tag.getNum($object_array[OOS['pos1']],'color')+'><b>'+$temp_+':</b></font> <font color=#666600><b>'+$str(kolvo_array[OOS['pos1']])+'</b></font><br>'
			killvar '$tempOOS_id',0
			jump 'for2'
		end
	end
elseif instr($OOS['obj'],'<keys>')!0:
!@ если выделенным объектом является связка ключей
	$color_=@em.tag.getNum($OOS['obj'],'color')
	$name_=@em.tag.getCont($OOS['obj'],'name')
	$name_=@get.word.padez($name_,'И')
	$print['key.table']+='<font color=#'+$color_+' face="CyrillicOld" size=+1><b>'+$name_+':</b></font><br>'
	if @get.daughter.obj($OOS['oid'],'<key>','$tempOOS_id')='true':
		$print['key.table']+= '<table cellspacing=0 cellpadding=5 bgcolor=#ffff88><tr><td align=left bgcolor=#bbbb44><font size=-3>'+@b.d.t('table.key')+':</font></td><td align=center bgcolor=#cccc55><font size=-3>'+@b.d.t('table.iner')+':</font></td><td align=center bgcolor=#bbbb44><font size=-3>'+@b.d.t('table.open')+':</font></td><td align=center bgcolor=#cccc55><font size=-3>'+@b.d.t('table.level')+':</font></td></tr>'
		:new
		if arrsize('$tempOOS_id')>0:
			OOS['pos1']=arrpos('$id_array',$tempOOS_id[0])
			$OOS['key'] = @em.tag.getCont($object_array[OOS['pos1']],'name')
			$OOS['key'] = @get.word.padez($OOS['key'],'И')
			if $OOS['key']='': $OOS['key'] = @b.d.t('table.noname')
			if @em.tag.getNum($object_array[OOS['pos1']],'open')!'':	$OOS['open'] = @em.tag.getNum($object_array[OOS['pos1']],'open')
			if kolvo_array[OOS['pos1']]!0:	$OOS['count'] = $str(kolvo_array[OOS['pos1']])
			if @em.tag.getNum($object_array[OOS['pos1']],'breake')!'':	$OOS['breake'] = @em.tag.getNum($object_array[OOS['pos1']],'breake')
			$print['key.table']+= '<tr><td align=left bgcolor=#dddd66><font size=-3>'+$OOS['key']+'</font></td><td align=center bgcolor=#eeee77><font size=-3>'+$OOS['count']+'</font></td><td align=center bgcolor=#dddd66><font size=-3>'+$OOS['open']+'</font></td><td align=center bgcolor=#eeee77><font size=-3>'+$OOS['breake']+'</font></td></tr>'
			killvar '$tempOOS_id',0
			jump 'new'
		end
		$print['key.table']+='</table>'
	end
elseif instr($OOS['obj'],'<diary>')!0:
!@ если выделенным объектом является дневник
	@int.diary($OOS['oid'])
	unselect
	exit
	!@ Выведение записей дневника, в т.ч. подзаданий, стадий задания и пр.
elseif instr($OOS['obj'],'<hero>')!0:
!@ если выделенным объектом является герой
	!@ Выводятся свойства героя
	killvar '$tempOOS_id'
	!@ выводит все свойства героя, кроме скрытых
	if @get.daughter.obj($OOS['oid'],'<property>','$tempOOS_id')='true':
		:property
		if arrsize('$tempOOS_id')>0:
			if @prv.str.inObj($tempOOS_id[0],'\[hide\]')='false': $print['hero.property']+=@int.hero.property($tempOOS_id[0])
			killvar '$tempOOS_id',0
			jump 'property'
		end
		killvar '$tempOOS_id'
	end
	!@ происходит вход в тело героя.
	$GAME_VALUE['inventory.floor'] = $OOS['oid']
	
	!@ Возможно организовать через меню было бы интереснее.
elseif instr($OOS['obj'],'<bag>')!0:
!@ если выделенным объектом является рюкзак
	!@ Производится выведение всех дочерних объектов рюкзака.
	$GAME_VALUE['inventory.floor'] = $OOS['oid']
	
	if $GAME_INTERFACE['подсказки']='show': $print['help']+=@game.help('предметы')+@b.w.s('help')
elseif instr($OOS['obj'],'<magic>')!0:
!@ если выделенным объектом является книга магии
	$GAME_VALUE['inventory.floor'] = $OOS['oid']
	
	if $GAME_INTERFACE['подсказки']='show': $print['help']+=@game.help('магия')+@b.w.s('help')
elseif instr($OOS['obj'],'<gate-stone>')!0:
!@ если выделенным объектом является ожерелье гроз
	$print['gs.table']+='<font color=#'+@em.tag.getNum($object_array[OOS['pos']],'color')+' face="CyrillicOld" size=+1><b>'+@b.d.t('gate-stone')+':</b></font><br>'
	if @get.daughter.obj($OOS['oid'],'<g-s>','$tempOOS_id')='true':
		$print['gs'] = '<table cellspacing=0 cellpadding=5 bgcolor=#ffff88><tr><td align=left bgcolor=#bbbb44><font size=-3>'+@b.d.t('target-point')+':</font></td><td align=center bgcolor=#cccc55><font size=-3>'+@b.d.t('table.iner')+':</font></td><td align=center bgcolor=#bbbb44><font size=-3>'+@b.d.t('table.charge')+':</font></td><td align=center bgcolor=#cccc55><font size=-3>'+@b.d.t('table.move')+':</font></td></tr>'
		:new1
		if arrsize('$tempOOS_id')>0:
			OOS['pos1']=arrpos('$id_array',$tempOOS_id[0])
			$OOS['key'] = @em.tag.getCont($object_array[OOS['pos1']],'name')
			if $OOS['key']='': $OOS['key'] = @b.d.t('g-s.neverland')
			if instr($object_array[OOS['pos1']],'[charge:')!0:	$OOS['open'] = $str(charge_array[OOS['pos1']])+'/'+@em.tag.getNum($object_array[OOS['pos1']],'charge') else $OOS['open']=''
			if kolvo_array[OOS['pos1']]!0: $OOS['count'] = $str(kolvo_array[OOS['pos1']])
			if ($OOS['open']!'' and charge_array[OOS['pos1']]!0) or $OOS['open']='': $OOS['breake']='<a href="exec:gs '+"'int.act.goto','"+$tempOOS_id[0]+"','"+$OOS['oid']+"'"+'">&#8594;</a>' else $OOS['breake']='<font color=#ff0000>'+@b.d.t('g-s.imposible')+'</font>'
			$print['gs.table'] += '<tr><td align=left bgcolor=#dddd66><font size=-3>'+$OOS['key']+'</font></td><td align=center bgcolor=#eeee77><font size=-3>'+$OOS['count']+'</font></td><td align=center bgcolor=#dddd66><font size=-3>'+$OOS['open']+'</font></td><td align=center bgcolor=#eeee77><font size=-3>'+$OOS['breake']+'</font></td></tr>'
			killvar '$tempOOS_id',0
			jump 'new1'
		end
		$print['gs.table']+='</table>'
	end
elseif instr($OOS['obj'],'<property>')!0:
!@ если выделенным объектом является свойство
	!@ Производится интерпретация свойства. В т.ч. если предусмотрено ссылка для прокачки.
	$print['hero.property']+=@int.hero.property($OOS['oid'])
elseif instr($OOS['obj'],'<spell>')!0:
!@ если выделенным объектом является заклинание
	$menu_OOS[arrsize('$menu_OOS')]=@b.d.t('menu.obj.spell.read')+':menu.obj.spell.read'
	$menu_OOS[arrsize('$menu_OOS')]=@b.d.t('menu.obj.read')+':menu.obj.read'
	gs 'int.menu.obj',$OOS['obj']
elseif $OOS['oid']='back':
!@ если выделенным объектом является возвратный объект
	$GAME_VALUE['inventory.floor'] = @em.tag.getCont($selobj,'back')
end
!@ скрипт, выполняемый при выделении объекта. Можно добить, например, меню, или что ещё
if $strfind($run_array[OOS['pos']],'!onObjSel!')<>'': $temp_ = @run.dynamic.script('!onObjSel!',$OOS['oid'])
!@ если присутствует скрипт
if arrsize('$menu_OOS')>0 and instr($OOS['obj'],'[lock.obj]')=0: menu "$menu_OOS"
unselect
@int.inventory
@true.goto.curloc($curloc)
--- onObjSel ---------------------------------