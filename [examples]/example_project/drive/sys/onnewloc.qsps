# onNewLoc
! Выполняется при посещении новой локации
! Для правильной работы интерпретатора требуется заблаговременное заполнение переменных:
!	$object_array[$curloc] - хранит заголовок локации, идентифицируется по её имени
!	$lvar['локация.исходник'] - хранит исходный текст локации, обрабатываемый различными интерпретаторами с текущей локации
!   ---------------- Используемые функции -------------------
!		int.loc.head - обрабатывает заголовок
!		int.din.text - обрабатывает описание локации
!		int.loc.obj - воспроизводит, сброшенные на локации объекты.
! Удаляем временные переменные
killvar '$int_act_goto'	&	!	поскольку переход совершён, этот массив нам больше не нужен

! ----------------- работа с полем "базовое описание локации" ------------------
	$SQUARE['SQUARE.loc.maintxt']=$MAINTXT
	$SQUARE['SQUARE.loc.curacts']=$CURACTS
	*clr
	cla
! ----------------- работа с полем "описание локации" ------------------

! определяем ай-ди текущей локации
$args['LID']=$func('get.loc.id')

	! -------------------- переход на новую локацию -----------
		if $id_watch[]<>$args['LID']: $id_watch[] = $args['LID']
		if arrsize('$id_watch')>100: killvar '$id_watch',0
	! -------------------- переход на новую локацию -----------

	! -------------------- переход на любую локацию -----------
		$refresh_watch[]=$args['LID']
		if arrsize('$refresh_watch')>9999: killvar '$refresh_watch',0
	! -------------------- переход на любую локацию -----------
	
if $refresh_watch[]<>$refresh_watch[arrsize('$refresh_watch')-2]:
	killvar '$dvar'
	gosub 'kill.var.olegus','$tvar','приветствие.'+$refresh_watch[arrsize('$refresh_watch')-2]
end

$GAME_VALUE['way.lock.by.enemy']=''	&	!	новая локация. Мы ещё не знаем заблокирована ли она врагом
! Передвигаем стрелки часов
if GAME_VALUE['goto'] = 1:
! запоминаем предыдущие показатели времени. Т.е. до изменения календарём
	time['last.sec']	=	time['sec']
	time['last.minute']	=	time['minute']
	time['last.hour']	=	time['hour']
	time['last.day']	=	time['day']
	time['last.date']	=	time['date']
	time['last.weekday']=	time['weekday']
	time['last.month']	=	time['month']
	time['last.year']	=	time['year']
	! генерация календаря
	gs 'int.time.calendar',$time['goto']
	! скрипты воздействий. Выполняются до генерации основного описания локации. Это позволяет дополнить исходник локации
	! каким-нибудь текстом, или вовсе подменить его на другой, если по выполнении скрипта должен проигрываться определённый сюжет.
	gs 'int.influence.scripts','!onNewLoc.property!',$time['goto']
end

! ------------------------------------------ эффекты, наложенные на локацию - предобработка -----------------------------------------------
	! предобработка позволяет добавить к исходнику локации дополнительный текст, а так же посчитать разные эффекы локации до обнуления переменных
	if $strfind($run_array[arrpos('$id_array',$args['LID'])],'!onNewLoc.pre!')!'': $args['run_array']=$func('run.dynamic.script','!onNewLoc.pre!',$args['LID'])
! ------------------------------------------ эффекты, наложенные на локацию - предобработка -----------------------------------------------

! Генерируется описание локации
! Для локаций всех типов описание из исходника:
$SQUARE['SQUARE.loc.fromSource']=$lvar['локация.исходник']

if $strfind($args['LID'],'(\[м:[\d]+\]_[\s\S]+|место;|place;)')!'':
! путём синтаксического разбора ай-ди локации установлено, что это локация места.

	! ----------- переход на новую локацию места -----------
		if $logist[]<>$args['LID']:
			$logist[]=$args['LID']	&	!	массив, где хранятся последние сто мест, где побывал герой
			! поскольку это новая локация места:
			$light_array[$args['LID']]=$func('int.lighting')		&	!	устанавливаем освещённость
			gosub 'magic.menergy.spell',$args['LID'],'','respawn'	&	!	респавним магическую энергию пространства
		end
		if arrsize('$logist')>100: killvar '$logist',0	&	!	если размер массива превышает 100 элементов, удаляем нулевой
	! ----------- переход на новую локацию места -----------

	if здесь_был[$args['LID']]<1:
	! метка о посещении выставляется при первом посещении
		здесь_был[$args['LID']]+=1
		! устанавливаем дефолтное значение магической энергии места:
		gosub 'magic.menergy.spell',$args['LID'],'','set'
	end
	
	$args['тип локации']='location'
	$SQUARE['SQUARE.loc.type']='место'	
	$args['действия на локации']=$func('int.loc.obj',$args['LID'],'','href','[count][weight][take:lift]')
	$SQUARE['SQUARE.location.obj.inActs']=$func('int.loc.obj',$args['LID'],'','acts','[count][weight][take:lift]')
	$SQUARE['SQUARE.location.obj.inHREF']=$func('int.loc.obj',$args['LID'],'','href','[count][weight][take:lift]')
	
	! выставляем настройки экрана в соответствии с освещением:
elseif $curloc='[chest]' and $strfind($args['LID'],'(\[с:[\d]+\]_[\s\S]+|сундук;|chest;)')<>'':
! путём синтаксического разбора установлено, что айди принадлежит локации-хранилищу
	! получаем заголовок:
	$args['head.chest']=$func('get.obj.id',$args['LID'])
	if $args['head.chest']='':
		! если заголовок не был создан
		! создаём заголовок и возвращаем его значение
		gosub 'gen.loc.head',$cvar['loc_id'+$cvar['chest']],$cvar['manage'+$cvar['chest']]
		$args['head.chest']=$func('get.obj.id',$args['LID'])
	end
	! получаем специализацию локации
	$args['spec.chest']=$func('em.tag.getCont',$args['head.chest'],'spec')
	
	if здесь_смотрел[$args['LID']]<1:
	! метка о посещении выставляется при первом посещении
		здесь_смотрел[$args['LID']]+=1
	end
	
	if $strfind($cvar['back'+$cvar['chest']],'(\[м:[\d]+\]_[\s\S]+|место;|place;)')!'':
	! если предыдущая локация была 
		$SQUARE['SQUARE.chest.closeAct']='<a class="plain" href="exec:killvar '+"'$cvar' & killvar 'smithing' & GOTO '"+$cvar['back'+$cvar['chest']]+"'"+'"><font color=#ff8800><b>'+$func('b.d.t','close','chest')+'</b></font></a>'
	elseif $strfind($cvar['back'+$cvar['chest']],'(\[с:[\d]+\]_[\s\S]+|сундук;|chest;)')!'' or $strfind($cvar['back'+$cvar['chest']],'(\[т:[\d]+\]_[\s\S]+|торговец;|seller;)')<>'':
		$SQUARE['SQUARE.chest.closeAct']='<a class="plain" href="exec:GOTO '+"'[chest]','"+$cvar['back'+$cvar['chest']]+"'"+'"><font color=#ff8800><b>'+$func('b.d.t','close','chest')+'</b></font></a>'
	end
	! Заметьте, удаление $cvar происходит только при активации ссылки "Закрыть", выбрасывающей на локацию места
	! Это связано с тем, что при удалении предмета из сундука происходит повторное обращение к локации сундука,
	! и исходные данные в этом случае должны быть сохранены. Так же на локации сундука могут быть другие локации сундука
	if instr($args['spec.chest'],'[smithing]')!0:
		$SQUARE['SQUARE.smithing.obj.inActs']=$func('int.loc.obj',$args['LID'],'','acts','[count][weight][take:take]')
		$SQUARE['SQUARE.smithing.obj.inHREF']=$func('int.loc.obj',$args['LID'],'','href','[count][weight][take:take]')
		$SQUARE['SQUARE.smithing.craftObjs']=$func('base.smithing',$cvar['loc_id'+$cvar['chest']])
		$SQUARE['SQUARE.loc.type']='кузница'
	elseif instr($args['spec.chest'],'[altar]')!0:
		$SQUARE['SQUARE.loc.type']='алтарь'
		$SQUARE['SQUARE.altar.sacrifice']=$func('int.altar',$cvar['loc_id'+$cvar['chest']])
	else
		$SQUARE['SQUARE.chest.obj.inHREF']=$func('int.loc.obj',$args['LID'],'','href','[count][weight][take:take]')	&	!	интерпретатор объектов на локации использует в данном случае не ай-ди локации, а ай-ди сундука.
		$SQUARE['SQUARE.loc.type']='хранилище'
	end
	$args['тип локации']='chest'
elseif $curloc='[chest]' and $strfind($args['LID'],'(\[т:[\d]+\]_[\s\S]+|торговец;|seller;)')<>'':
	! локация торговли
	$args['тип локации']='seller'
	$SQUARE['SQUARE.loc.type']='seller'
	
	args['$']=arrpos('$id_array',$args['LID'])
	if здесь_торговал[$args['LID']]<1:
		здесь_торговал[$args['LID']]+=1
	end
	! респавн торговца
		if $func('sim.time',$respawn_array[args['$']])!'first':
		! если подошло время респавна
			args['$bank']=func('get.tag.num',$object_array[args['$']],'bank')	&	!	вычисляем каков банк торговца
			if args['$bank']=0: args['$bank']=1000
			$args['$respawn']=$func('em.tag.getCont',$object_array[args['$']],'respawn')	&	!	получаем время респавна
			if $args['$respawn']='': $args['$respawn']='[day:7]'
			if charge_array[args['$']]<=args['$bank']:
			!	если денег в банке меньше либо равно, чем должно быть, накидываем денег
				charge_array[args['$']]=args['$bank']
			else
			! если денег в банке больше, чем должно быть, вычитаем якобы растраченную часть. Для этого:
				args['$respawn.time']=$func('get.tag.num',$func('conv.time',$args['$respawn'],'minute'),'minute')	&	!	вычисляем период респавнинга в минутах
				args['$time.now']=$func('get.tag.num',$func('conv.time',$func('get.time.now'),'minute'),'minute')	&	!	вычисляем текущее время в минутах
				args['$respawn.last']=$func('get.tag.num',$func('conv.time',$respawn_array[args['$']],'minute'),'minute')	&	!	вычисляем время последнего респавна
				charge_array[args['$']]-=(args['$time.now']-args['$respawn.last'])*(charge_array[args['$']]-args['$bank'])/args['$respawn.time']
			end
			$respawn_array[args['$']]=$func('summ.time',$func('get.time.now'),$args['$respawn'])
		end
	! респавн торговца
	if $strfind($cvar['back'+$cvar['chest']],'(\[м:[\d]+\]_[\s\S]+|место;|place;)')!'':
		$SQUARE['SQUARE.seller.closeAct']='&nbsp;&nbsp;&nbsp;&nbsp;<div align=left><a class="plain" href="exec:killvar '+"'$cvar' & GOTO '"+$cvar['back'+$cvar['chest']]+"'"+'"><font color=#ff8800><b>.'+$func('b.d.t','close','seller')+'</b></font></a></div>'
	elseif $strfind($cvar['back'+$cvar['chest']],'(\[т:[\d]+\]_[\s\S]+|торговец;|seller;)')<>'' or $strfind($cvar['back'+$cvar['chest']],'(\[с:[\d]+\]_[\s\S]+|сундук;|chest;)')!'':
		$SQUARE['SQUARE.seller.closeAct']='&nbsp;&nbsp;&nbsp;&nbsp;<div align=left><a class="plain" href="exec:GOTO '+"'[chest]','"+$cvar['back'+$cvar['chest']]+"'"+'"><font color=#ff8800><b>'+$func('b.d.t','close','seller')+'</b></font></a></div>'
	end
	$SQUARE['SQUARE.seller.obj.inActs']=$func('int.loc.obj',$args['LID'],'','acts','[count][take:buy][weight][cost]')
	$SQUARE['SQUARE.seller.obj.inHREF']=$func('int.loc.obj',$args['LID'],'','href','[count][take:buy][weight][cost]')
end
! ----- динамические скрипты локации. Поствыполнение ------
	if $strfind($run_array[arrpos('$id_array',$args['LID'])],'!onNewLoc.post!')!'': $args['кккккк2139']=$func('run.dynamic.script','!onNewLoc.post!',$args['LID'])
! ----- динамические скрипты локации. Поствыполнение ------

! ------------------------------ заносим в основное описание дополнительные тексты ----------------------------
$SQUARE['SQUARE.loc.hallow']=$tvar['приветствие.'+$args['LID']]
$SQUARE['SQUARE.loc.plustext']=$lvar['локация.дополнительные тексты']
! -массив $dvar
$SQUARE['SQUARE.loc.dvar']=''
args['d']=0
:print_Dvar
if args['d']<arrsize('$dvar'):
	if $dvar[args['d']]<>'':
		$SQUARE['SQUARE.loc.dvar']+=$dvar[args['d']]+'<br>'
	end
	args['d']+=1
	jump 'print_Dvar'
end
! ------------------------------ заносим в основное описание дополнительные тексты ----------------------------

if $GAME_INTERFACE['обучение']='show' and $func('[help]',$args['LID'])!'': $print['help']+="<br>"+$func('[help]',$args['LID'])
$SQUARE['SQUARE.loc.acts.inActs']=$func('int.loc.act','acts')
$SQUARE['SQUARE.loc.acts.inHREF']=$func('int.loc.act','href')
$SQUARE['SQUARE.head']=$func('int.loc.head',$args['LID'],$args['тип локации'])	&	!	интерпретатор заголовков локаций
$SQUARE['SQUARE.stat']=$func('print.word')
! теперь когда получены все возможные данные, запускаем интерпретатор интерфейса
gosub 'int.loc.interface',$args['LID'],$args['тип локации']
! "---- 
	Вставить здесь задержку и вывод пустой строки для случаев, когда мы посещаем одну и ту же локацию и нам не нужно читать описание по-новой.
!"
! ----- обнуление данных --------------------------------------------------------------------------------------------------------
$time['goto']=''
GAME_VALUE['goto'] = 0
$kolvo_array[arrpos('$id_array',$args['LID'])]=$time['new.all']
killvar '$result'
killvar '$lvar'
killvar '$avar'
killvar 'time_goto'
--- onNewLoc ---------------------------------