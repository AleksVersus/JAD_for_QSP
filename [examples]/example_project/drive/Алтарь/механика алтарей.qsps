QSP-Game Интерпретатор алтаря.
$[0]	&	!	ай-ди локации
# int.altar
local $loc_id_, $temp_ = $args[0], ''
local loc_obj_count_, $on_altar_id_, $loc_head_, $obj_list_, $charged_link_, $altar_
local $karma_, $charge_, $action_
!@ получаем заголовок локации
$loc_head_ = @get.obj.id($loc_id_)
!@ получаем список предметов, сброшенных на локации.
$obj_list_ = @int.loc.obj($loc_id_, '', 'href', '[count][take:take][spirit]')

!@ ---------------- получаем идентификатор предмета, установленный на подзарядку -----------------------
	local $id_obj_IAO_
	$temp_ = @get.daughter.obj($loc_id_,'','$id_obj_IAO_')
	loc_obj_count_ = arrsize('$id_obj_IAO_')
	if $temp_ = 'true':
		loop while arrsize('$id_obj_IAO_')>0 step killvar '$id_obj_IAO_',0:
			if $strfind($run_array[arrpos('$id_array',$id_obj_IAO_[0])],'!<onALTAR>')<>'':
				$on_altar_id_ = $id_obj_IAO_[0]
				jump 'Really_Unimaginable_Stories'
			end
		end
		:Really_Unimaginable_Stories
	end
!@ ---------------- получаем идентификатор предмета, установленный на подзарядку -----------------------

!@ если такой прдмет имеется
if $on_altar_id_<>'':
	!@ получаем ссылку етого предмета
	$temp_['link'] = $strfind($obj_list_, "<a class=""plain"" href=""exec:gs @take.obj('<<$on_altar_id_>>') & @true.goto.curloc('\[chest\]')""><font color=#[A-Fa-f0-9]{6}>[^<]+</font></a><br>")
	$temp_['obj'] = $object_array[arrpos('$id_array',$on_altar_id_)]
	$temp_['name']=@get.word.padez(@em.tag.getCont($temp_['obj'],'name'),'В')
	$temp_['color']=@em.tag.getNum($temp_['obj'],'color')
	$charged_link_ = '<b><a class="plain" href="exec:gs ''take.obj'','''+$on_altar_id_+''' & GS ''true.goto.curloc'',''[chest]''"><font color=#'+$temp_['color']+'>'+@b.d.t('altar.remove',$temp_['name'])+'</font></a></b>'
	$altar_+='<font size=-1>'+@b.d.t('altar.credence')+':</font><br>'
	$altar_+=$replace($obj_list_,'&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;'+$temp_['link'])
else
	$altar_='<font size=-1>'+@b.d.t('altar.credence')+':</font><br>'+$obj_list_
end
!@ Если локация отведена определённому божеству, можно сгенерировать это божество
$temp_['god'] = $trim(@em.tag.getCont($loc_head_, 'god'))
$temp_['god_id'] = @get.id.obj('(<god>[\s\S]*\[god:'+$temp_['god']+':god\]|\[god:'+$temp_['god']+':god\][\s\S]*<god>)')
if $temp_['god_id'] = 'false':
	$temp_['god_id'] = @add.new.obj('<god> [god:'+$temp_['god']+':god] [name:'+$temp_['god']+':name]','','GOD')
end
$karma_ = @em.indiv(kolvo_array[arrpos('$id_array',$temp_['god_id'])],100,10)

!@ ------------------------------- выборка заряда --------------------------------
if kolvo_array[arrpos('$id_array',$temp_['god_id'])]/100!0 and $on_altar_id_<>'':
	! здесь следует вычислять текущую стоимость
	$charge_ = '<a class="plain" href="exec:gs ''altar.charge.obj'',''<<$loc_id_>>'',''<<$on_altar_id_>>'' & gs ''true.goto.curloc'',$curloc"><b><font color=#996600>'+@b.d.t('altar.charge')+'</font></b></a>'
else:
	$charge_ = '<font color=#888888>'+@b.d.t('altar.charge')+'</font>'
end
!@ ------------------------------- выборка заряда --------------------------------

if ($on_altar_id_<>'' and loc_obj_count_>1) or (loc_obj_count_>0 and $on_altar_id_=''):
	$action_+='<a class="plain" href="exec:gs ''altar.sacrifice'',''<<$loc_id_>>'' & gs ''true.goto.curloc'',$curloc"><b><font color=#996600>'+@b.d.t('altar.sacrifice')+'</font></b></a>'
else
	$action_+='<font color=#888888>'+@b.d.t('altar.sacrifice')+'</font>'
end
!@ создаём нужное
$result += '<table border=0 width=100% cellpadding=5>'
$result += '<tr><td align=right valign=middle>'+@b.d.t('altar.karma')+': </td><td align=left valign=middle width=35%><<$karma_>></td></tr>'
$result += '<tr><td align=left valign=middle><<$charged_link_>></td><td align=center valign=middle width=35%><<$charge_>></td></tr>'
$result += '<tr><td align=left valign=top><<$altar_>></td><td align=center valign=bottom width=35%><<$action_>></td></tr>'
$result += '</table>'
--- int.altar ---------------------------------

QSP-Game приносит предметы в жертву
$[0]	&	!	id локации
# altar.sacrifice
local $loc_id_ = $args[0]
local $loc_head_, $temp_, karma_add_, prayer_add_, $god_id_
$loc_head_=@get.obj.id($loc_id_)
!@ получаем предмет, установленный на подзарядку
local $id_obj_AS_
$temp_ = @get.daughter.obj($loc_id_,'','$id_obj_AS_')
if $temp_ = 'true':
	loop local pos_ = -1 while arrsize('$id_obj_AS_') <> 0 step killvar '$id_obj_AS_',0:
		pos_ = arrpos('$id_array',$id_obj_AS_[0])
		if instr($run_array[pos_], '!<onALTAR>') = 0:
			karma_add_ += @get.spirit($id_obj_AS_[0])
			prayer_add_ += 1
			@del.obj($id_obj_AS_[0])
		end
	end
end
$temp_ = @em.tag.getCont($loc_head_,'god')
$god_id_ = @get.id.obj('(<god>[\s\S]*\[god:<<$temp_>>:god\]|\[god:<<$temp_>>:god\][\s\S]*<god>)')
kolvo_array[arrpos('$id_array', $god_id_)] += karma_add_
charge_array[arrpos('$id_array', $god_id_)] += prayer_add_
$print['charge.altar'] += @b.w.s('altar.sacrifice', prayer_add_)
--- altar.sacrifice ---------------------------------

QSP-Game локация производит заряд предмета от кармической энергии, если это возможно.
$[0]	&	!@ идентификатор локации
$[1]	&	!@ идентификатор предмета
# altar.charge.obj
local $loc_id_, $obj_id_, $temp_ = $args[0], $args[1], ''
local $loc_head_, pos_, charge_, $god_, $god_id_, karma_add_, unit_karma_, how_mach_karma_
!@ получаем заголовок локации
$loc_head_ = @get.obj.id($loc_id_)
!@ получаем заряжаемый предмет:
if $obj_id_ <> '':
	!@ если предмет помещён в жертвенник
	!@ получаем позицию в базе и тело предмета
	pos_ = arrpos('$id_array', $obj_id_)
	!@ получаем максимальный заряд предмета и текущий заряд предмета
	charge_['max'] = @em.tag.getNum($object_array[pos_],'maxchrg')	&	!@ 5000 (500000)
	charge_['cur'] = charge_array[pos_]	&	!@ 300 (30000)
	charge_['to_fill'] = charge_['max'] - charge_['cur']	&	!@ сколько требуется до полной зарядки 4700 (470000)
	if charge_['to_fill'] = 0:
		$print['charge.altar']+=@b.w.s('altar.charge','dont')
		exit
	end
	!@ получаем ай-ди божества
	$god_ = $trim(@em.tag.getCont($loc_head_,'god'))
	$god_id_ = @get.id.obj('(<god>[\s\S]*\[god:<<$god_>>:god\]|\[god:<<$god_>>:god\][\s\S]*<god>)')
	god_pos_ = arrpos('$id_array', $god_id_)
	!@ получаем кармическую энергию, переданную божеству
	karma_add_ = kolvo_array[god_pos_]	&	!@ 7333 (733300)
	!@ одна единица заряда предмета (100) потребляет определённое количество кармической энергии 5(500) по умолчанию.
	unit_karma_ = 500	&	!@ ЗДЕСЬ ДОЛЖНО ПРОИСХОДИТЬ ОБРАЩЕНИЕ К ФОРМУЛЕ, ВЫЧИСЛЯЮЩЕЙ СТОИМОСТЬ ЕДИНИЦЫ ЗАРЯДА В КАРМИЧЕСКОЙ ЭНЕРГИИ
	!@ сколько заряда максимум мы можем из этого выжать
	how_mach_karma_=(karma_add_*100)/unit_karma_
	if how_mach_karma_>=charge_['to_fill']:
	!@ если количество заряда, который можно получить, достаточно для заряда всего посоха, заряжаем весь
		charge_array[pos_] += charge_['to_fill']
		!@ уменьшаем количество кармической энергии
		kolvo_array[god_pos_] -= charge_['to_fill'] * unit_karma_/100
		$print['charge.altar'] += @b.w.s('altar.charge','full')
	else
	!@ если количество заряда, который можно получить, достаточно только для части посоха, заряжаем часть
		charge_array[pos_] += how_mach_karma_
		!@ уменьшаем количество кармической энергии
		kolvo_array[god_pos_] -= how_mach_karma_*unit_karma_/100
		$print['charge.altar'] += @b.w.s('altar.charge', 'part', $obj_id_)
	end
end
--- altar.charge.obj ---------------------------------

функция получает "духовную стоимость" предмета. Духовную ценность можно указывать для определённого божества.
$[0]	&	!	ай-ди предмета
$[1]	&	!	ай-ди локации

# get.spirit
local $obj_id_, $loc_id_ = $args[0], $args[1]
local $obj_body_, $loc_body_, $temp_, spirit_charge_, spirit_
$obj_body_ = @get.obj.id($obj_id_)
pos_ = arrpos('$id_array',$obj_id_)
$loc_body_ = @get.obj.id($loc_id_)
!@ получаем имя бога, которому должно делаться жертвоприношение
$temp_ = @em.tag.getCont($loc_body_,'god')
!@ получаем духовную ценность предмета для этого конкретного бога
spirit_charge_ = @em.tag.getNum(@em.tag.getCont($obj_body_,'spirit'), $temp_)
!@ если духовная ценность для этого конкретно божества не указана, получаем духовную ценность для "пустого" бога
if spirit_charge_ = 0:
	spirit_ = @em.tag.getNum($obj_body_,'spirit')
else
	spirit_ = spirit_charge_
end
!@ если и на этот раз мы не обнаружили духовную ценность, вычисляем духовную ценность из всяких свойств предмета:
if spirit_ = 0:
	spirit_ = @em.tag.getNum($obj_body_,'stoim')/5
end
if spirit_ = 0:
	spirit_ = @em.tag.getNum($obj_body_,'weight')/50

	local $spirit_type_=@em.tag.getCont($obj_body_,'np')
	!@ поправить коэффициенты согласно балансу
	if instr($spirit_type_,'[оружие]')!0: spirit_+=100
	if instr($spirit_type_,'[двуручное]')!0: spirit_+=80
	if instr($spirit_type_,'[одноручное]')!0: spirit_+=60
	if instr($spirit_type_,'[топор]')!0: spirit_+=40
	if instr($spirit_type_,'[молот]')!0: spirit_+=40
	if instr($spirit_type_,'[меч]')!0: spirit_+=60
	if instr($spirit_type_,'[лук]')!0: spirit_+=100
	if instr($spirit_type_,'[посох]')!0: spirit_+=500
	if instr($spirit_type_,'[огнестрельное]')!0: spirit_+=200
	if instr($spirit_type_,'[нож]')!0: spirit_+=20
	if instr($spirit_type_,'[булава]')!0: spirit_+=40
	if instr($spirit_type_,'[кинжал]')!0: spirit_+=40
	if instr($spirit_type_,'[доспех]')!0: spirit_+=100
	if instr($spirit_type_,'[шлем]')!0: spirit_+=20
	if instr($spirit_type_,'[наплечни]')!0: spirit_+=80
	if instr($spirit_type_,'[кираса]')!0: spirit_+=100
	if instr($spirit_type_,'[перчатки]')!0: spirit_+=20
	if instr($spirit_type_,'[поножи]')!0: spirit_+=100
	if instr($spirit_type_,'[обувь]')!0: spirit_+=80
	if instr($spirit_type_,'[амулет]')!0: spirit_+=500
	if instr($spirit_type_,'[щит]')!0: spirit_+=100
	if instr($spirit_type_,'[снаряды]')!0: spirit_+=50
	if instr($spirit_type_,'[болты]')!0: spirit_+=2
	if instr($spirit_type_,'[стрелы]')!0: spirit_+=1
	if instr($spirit_type_,'[снаряд метательный]')!0: spirit_+=5
	if instr($spirit_type_,'[ингредиент]')!0: spirit_+=15
	if instr($spirit_type_,'[еда]')!0: spirit_+=5
	if instr($spirit_type_,'[свиток]')!0: spirit_+=75
	if instr($spirit_type_,'[книга]')!0: spirit_+=50
	if instr($spirit_type_,'[шкатулка]')!0: spirit_+=25
	if instr($spirit_type_,'[материал]')!0: spirit_+=1
	if instr($spirit_type_,'[меновой]')!0: spirit_+=1
end
result = spirit_ * kolvo_array[pos_]
--- get.spirit ---------------------------------

Надстройка для сброса предмета.  Эта метка мешает алтарю уничтожить предмет как жертвенный
# menu.obj.put.onAltar
if $args[0]='': $args[0]=$OOS['oid']
$run_array[arrpos('$id_array',$args[0])] += {!<onALTAR>
if $args[0]='!passed!':	$print['put.altar'] += @b.w.s('put.altar',$args[1])
if $args[0]='!taked!':
	local pos_=arrpos('$id_array',$args[1])
	local $cud_code_ = $strfind($run_array[pos_], '!<'+'onALTAR'+'>[\s\S]*!<\/'+'onALTAR'+'>')
	$run_array[pos_] = $replace($run_array[pos_], $cud_code_)
end
!</onALTAR>
}
@menu.obj.put($args[0],1)
--- menu.obj.put.onAltar ---------------------------------