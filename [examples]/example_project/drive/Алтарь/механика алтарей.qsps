# int.altar
! Интерпретатор алтаря.
$args[0] = $args[0]	&	!	ай-ди локации
! получаем заголовок локации
$args['loc.head']=$func('get.obj.id',$args[0])
! получаем список предметов, сброшенных на локации.
$args['предметы на алтаре']=$func('int.loc.obj',$args[0],'','href','[count][take:take][spirit]')

! ---------------- получаем идентификатор предмета, установленный на подзарядку -----------------------
	$args['onAltar']=$func('get.daughter.obj',$args[0],'','$id_obj_IAO')
	args['size.MASS']=arrsize('$id_obj_IAO')
	if $args['onAltar']='true':
		:Really_Unimaginable_Stories
		if arrsize('$id_obj_IAO')>0:
			if $strfind($run_array[arrpos('$id_array',$id_obj_IAO[0])],'!<onALTAR>')<>'':
				$args['onAltar.ID']=$id_obj_IAO[0]
			else
				killvar '$id_obj_IAO',0
				jump 'Really_Unimaginable_Stories'
			end
		end
	end
	killvar '$id_obj_IAO'
! ---------------- получаем идентификатор предмета, установленный на подзарядку -----------------------

! если такой прдмет имеется
if $args['onAltar.ID']<>'':
	! получаем ссылку етого предмета
	$args['ссылка етого предмета']=$strfind($args['предметы на алтаре'],'<a class="plain" href="exec:gs ''take.obj'','''+$args['onAltar.ID']+''' & GS ''true.goto.curloc'',''\[chest\]''"><font color=#[A-Fa-f0-9]{6}>[^<]+</font></a><br>')
	$args['obj.qwerty']=$object_array[arrpos('$id_array',$args['onAltar.ID'])]
	$args['name.qwerty']=$func('get.word.padez',$func('get.tag.cont',$args['obj.qwerty'],'name'),'В')
	$args['color.qwerty']=$func('get.tag.num',$args['obj.qwerty'],'color')
	$args['заряжаемый']='<b><a class="plain" href="exec:gs ''take.obj'','''+$args['onAltar.ID']+''' & GS ''true.goto.curloc'',''[chest]''"><font color=#'+$args['color.qwerty']+'>'+$func('b.d.t','altar.remove',$args['name.qwerty'])+'</font></a></b>'
	$args['жертвенник']+='<font size=-1>'+$func('b.d.t','altar.credence')+':</font><br>'
	$args['жертвенник']+=$replace($args['предметы на алтаре'],'&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;'+$args['ссылка етого предмета'])
else
	$args['жертвенник']='<font size=-1>'+$func('b.d.t','altar.credence')+':</font><br>'+$args['предметы на алтаре']
end
! Если локация отведена определённому божеству, можно сгенерировать это божество
$args['god.altar']=TRIM($func('get.tag.cont',$args['loc.head'],'god'))
$args['god.id']=$func('get.id.obj','(<god>[\s\S]*\[god:'+$args['god.altar']+':god\]|\[god:'+$args['god.altar']+':god\][\s\S]*<god>)')
if $args['god.id']='false':
	$args['god.id']=$func('add.new.obj','<god> [god:'+$args['god.altar']+':god] [name:'+$args['god.altar']+':name]','','GOD')
end
$args['карма+']=$func('#indiv#',kolvo_array[arrpos('$id_array',$args['god.id'])],100,10)

! ------------------------------- выборка заряда --------------------------------
if kolvo_array[arrpos('$id_array',$args['god.id'])]/100!0 and $args['onAltar.ID']<>'':
	! здесь следует вычислять текущую стоимость
	$args['зарядить+']='<a class="plain" href="exec:gs ''altar.charge.obj'',''<<$args[0]>>'',''<<$args[''onAltar.ID'']>>'' & gs ''true.goto.curloc'',$curloc"><b><font color=#996600>'+$func('b.d.t','altar.charge')+'</font></b></a>'
else
	$args['зарядить+']='<font color=#888888>'+$func('b.d.t','altar.charge')+'</font>'
end
! ------------------------------- выборка заряда --------------------------------

if ($args['onAltar.ID']<>'' and args['size.MASS']>1) or (args['size.MASS']>0 and $args['onAltar.ID']=''):
	$args['действие+']+='<a class="plain" href="exec:gs ''altar.sacrifice'',''<<$args[0]>>'' & gs ''true.goto.curloc'',$curloc"><b><font color=#996600>'+$func('b.d.t','altar.sacrifice')+'</font></b></a>'
else
	$args['действие+']+='<font color=#888888>'+$func('b.d.t','altar.sacrifice')+'</font>'
end
! создаём нужное
$args['описание локации']+='<table border=0 width=100% cellpadding=5>'
$args['описание локации']+='<tr><td align=right valign=middle>'+$func('b.d.t','altar.karma')+': </td><td align=left valign=middle width=35%><<$args["карма+"]>></td></tr>'
$args['описание локации']+='<tr><td align=left valign=middle>'+$args['заряжаемый']+'</td><td align=center valign=middle width=35%>'+$args['зарядить+']+'</td></tr>'
$args['описание локации']+='<tr><td align=left valign=top>'+$args['жертвенник']+'</td><td align=center valign=bottom width=35%>'+$args['действие+']+'</td></tr>'
$args['описание локации']+='</table>'
$result = $args['описание локации']
killvar '$id_obj_IAO'
--- int.altar ---------------------------------

# altar.sacrifice
! приносит предметы в жертву
$args[0] = $args[0]	&	!	id локации
$args['loc.head']=$func('get.obj.id',$args[0])
! получаем список предметов, сброшенных на локации.
$args['предметы на алтаре']=$func('int.loc.obj',$args[0])
! получаем предмет, установленный на подзарядку
$args['altar.obj']=$func('get.daughter.obj',$args[0],'','$id_obj_AS')
if $args['altar.obj']='true':
	:for
	if arrsize('$id_obj_AS')!0:
		args['pit'] = arrpos('$id_array',$id_obj_AS[0])
		if instr($run_array[args['pit']],'!<onALTAR>')=0:
			args['+к карме']+=func('get.spirit',$id_obj_AS[0])
			args['+к молитвам']+=1
			gosub 'del.obj',$id_obj_AS[0]
		end
		killvar '$id_obj_AS',0
		jump 'for'
	end
end
$args['god.altar']=$func('get.tag.cont',$args['loc.head'],'god')
$args['god.id']=$func('get.id.obj','(<god>[\s\S]*\[god:'+$args['god.altar']+':god\]|\[god:'+$args['god.altar']+':god\][\s\S]*<god>)')
kolvo_array[arrpos('$id_array',$args['god.id'])]	+=	args['+к карме']
charge_array[arrpos('$id_array',$args['god.id'])]	+=	args['+к молитвам']
$print['charge.altar']+=$func('b.w.s','altar.sacrifice',args['+к молитвам'])
--- altar.sacrifice ---------------------------------

# altar.charge.obj
! локация производит заряд предмета от кармической энергии, если это возможно.
$args[0] = $args[0]	&	!	идентификатор локации
$args[1] = $args[1]	&	!	идентификатор предмета
! получаем заголовок локации
$args['loc.head']=$func('get.obj.id',$args[0])
! получаем заряжаемый предмет:
if $args[1]<>'':
	! если предмет помещён в жертвенник
	$args['onAltar']=$args[1]
	! получаем позицию в базе и тело предмета
	args['pit']=arrpos('$id_array',$args['onAltar'])
	$args['obj']=$object_array[args['pit']]
	! получаем максимальный заряд предмета и текущий заряд предмета
	args['max_charge']=func('get.tag.num',$args['obj'],'maxchrg')	&	!	5000 (500000)
	args['charge']=charge_array[args['pit']]	&	!	300 (30000)
	args['full']=args['max_charge']-args['charge']	&	!	сколько требуется до полной зарядки 4700 (470000)
	if args['full']=0:
		$print['charge.altar']+=$func('b.w.s','altar.charge','dont')
		exit
	end
	! получаем ай-ди божества
	$args['god.altar']=TRIM($func('get.tag.cont',$args['loc.head'],'god'))
	$args['god.id']=$func('get.id.obj','(<god>[\s\S]*\[god:'+$args['god.altar']+':god\]|\[god:'+$args['god.altar']+':god\][\s\S]*<god>)')
	args['god.pit']=arrpos('$id_array',$args['god.id'])
	! получаем кармическую энергию, переданную божеству
	args['карма+']=kolvo_array[args['god.pit']]	&	!	7333 (733300)
	! одна единица заряда предмета (100) потребляет определённое количество кармической энергии 5(500) по умолчанию.
	args['кармы на единицу заряда']=500	&	!	ЗДЕСЬ ДОЛЖНО ПРОИСХОДИТЬ ОБРАЩЕНИЕ К ФОРМУЛЕ, ВЫЧИСЛЯЮЩЕЙ СТОИМОСТЬ ЕДИНИЦЫ ЗАРЯДА В КАРМИЧЕСКОЙ ЭНЕРГИИ
	! сколько заряда максимум мы можем из этого выжать
	args['сколько можно получить']=(args['карма+']*100)/args['кармы на единицу заряда']
	if args['сколько можно получить']>=args['full']:
	! если количество заряда, который можно получить, достаточно для заряда всего посоха, заряжаем весь
		charge_array[args['pit']]+=args['full']
		! уменьшаем количество кармической энергии
		kolvo_array[args['god.pit']]-=args['full']*args['кармы на единицу заряда']/100
		$print['charge.altar']+=$func('b.w.s','altar.charge','full')
	else
	! если количество заряда, который можно получить, достаточно только для части посоха, заряжаем часть
		charge_array[args['pit']]+=args['сколько можно получить']
		! уменьшаем количество кармической энергии
		kolvo_array[args['god.pit']]-=args['сколько можно получить']*args['кармы на единицу заряда']/100
		$print['charge.altar']+=$func('b.w.s','altar.charge','part',$args['onAltar'])
	end
end
--- altar.charge.obj ---------------------------------

# get.spirit
! функция получает "духовную стоимость" предмета. Духовную ценность можно указывать для определённого божества.
$args[0] = $args[0]	&	!	ай-ди предмета
$args[1] = $args[1]	&	!	ай-ди локации
$args['obj']=$func('get.obj.id',$args[0])
args['pit'] = arrpos('$id_array',$args[0])
$args['loc']=$func('get.obj.id',$args[1])
! получаем имя бога, которому должно делаться жертвоприношение
$args['god.name']=$func('get.tag.cont',$args['loc'],'god')
! получаем духовную ценность предмета для этого конкретного бога
args['spirit.god.name']=func('get.tag.num',$func('get.tag.cont',$args['obj'],'spirit'),$args['god.name'])
! если духовная ценность для этого конкретно божества не указана, получаем духовную ценность для "пустого" бога
if args['spirit.god.name']=0:
	args['spirit']=func('get.tag.num',$args['obj'],'spirit')
else
	args['spirit']=args['spirit.god.name']
end
! если и на этот раз мы не обнаружили духовную ценность, вычисляем духовную ценность из всяких свойств предмета:
if args['spirit']=0:
	args['spirit']=func('get.tag.num',$args['obj'],'stoim')/5
end
if args['spirit']=0:
	args['spirit']=func('get.tag.num',$args['obj'],'weight')/50

	$args['spirit.type']=$func('get.tag.cont',$args['obj'],'np')
	! поправить коэффициенты согласно балансу
	if instr($args['spirit.type'],'[оружие]')!0: args['spirit']+=100
	if instr($args['spirit.type'],'[двуручное]')!0: args['spirit']+=80
	if instr($args['spirit.type'],'[одноручное]')!0: args['spirit']+=60
	if instr($args['spirit.type'],'[топор]')!0: args['spirit']+=40
	if instr($args['spirit.type'],'[молот]')!0: args['spirit']+=40
	if instr($args['spirit.type'],'[меч]')!0: args['spirit']+=60
	if instr($args['spirit.type'],'[лук]')!0: args['spirit']+=100
	if instr($args['spirit.type'],'[посох]')!0: args['spirit']+=500
	if instr($args['spirit.type'],'[огнестрельное]')!0: args['spirit']+=200
	if instr($args['spirit.type'],'[нож]')!0: args['spirit']+=20
	if instr($args['spirit.type'],'[булава]')!0: args['spirit']+=40
	if instr($args['spirit.type'],'[кинжал]')!0: args['spirit']+=40
	if instr($args['spirit.type'],'[доспех]')!0: args['spirit']+=100
	if instr($args['spirit.type'],'[шлем]')!0: args['spirit']+=20
	if instr($args['spirit.type'],'[наплечни]')!0: args['spirit']+=80
	if instr($args['spirit.type'],'[кираса]')!0: args['spirit']+=100
	if instr($args['spirit.type'],'[перчатки]')!0: args['spirit']+=20
	if instr($args['spirit.type'],'[поножи]')!0: args['spirit']+=100
	if instr($args['spirit.type'],'[обувь]')!0: args['spirit']+=80
	if instr($args['spirit.type'],'[амулет]')!0: args['spirit']+=500
	if instr($args['spirit.type'],'[щит]')!0: args['spirit']+=100
	if instr($args['spirit.type'],'[снаряды]')!0: args['spirit']+=50
	if instr($args['spirit.type'],'[болты]')!0: args['spirit']+=2
	if instr($args['spirit.type'],'[стрелы]')!0: args['spirit']+=1
	if instr($args['spirit.type'],'[снаряд метательный]')!0: args['spirit']+=5
	if instr($args['spirit.type'],'[ингредиент]')!0: args['spirit']+=15
	if instr($args['spirit.type'],'[еда]')!0: args['spirit']+=5
	if instr($args['spirit.type'],'[свиток]')!0: args['spirit']+=75
	if instr($args['spirit.type'],'[книга]')!0: args['spirit']+=50
	if instr($args['spirit.type'],'[шкатулка]')!0: args['spirit']+=25
	if instr($args['spirit.type'],'[материал]')!0: args['spirit']+=1
	if instr($args['spirit.type'],'[меновой]')!0: args['spirit']+=1
end
result = args['spirit'] * kolvo_array[args['pit']]
--- get.spirit ---------------------------------

# menu.obj.put.onAltar
! надстройка для сброса предмета.  Эта метка мешает алтарю уничтожить предмет как жертвенный
if $args[0]='': $args[0]=$OOS['oid']
$run_array[arrpos('$id_array',$args[0])]+={!<onALTAR>
$args[0] = $args[0]
$args[1] = $args[1]
$args[2] = $args[2]
if $args[0]='!passed!':	$print['put.altar']+=$func('b.w.s','put.altar',$args[1])
if $args[0]='!taked!':
	args['pit']=arrpos('$id_array',$args[1])
	$args['run.onAltar']=$strfind($run_array[args['pit']],'!<'+'onALTAR'+'>[\s\S]*!<\/'+'onALTAR'+'>')
	$run_array[args['pit']]=$replace($run_array[args['pit']],$args['run.onAltar'])
end
!</onALTAR>
}
gs 'menu.obj.put',$args[0],1
--- menu.obj.put.onAltar ---------------------------------