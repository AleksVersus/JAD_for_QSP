QSP-Game Общий алгоритм восстановлений
! поскольку все восстановления по сути расчитываются по одинаковым формулам, можно использовать один и тот же алгоритм
! данный алгоритм предназначен для восстановления с течением времени
$args[0]
$args[1]
# hero.vosst.time
!@ объявляем локальные переменные
local $obj_body_, $property_, $p_name_
local $gt_last_, frequenсy_, gt_secs_, pik_, rest_
local hours_, minutes_, $texts_

$obj_body_ = $object_array[arrpos('$id_array',$args[0])]
$property_ = @em.tag.getCont($obj_body_)
$p_name_ = @b.d.t('property_name', $property_)
!@ суммируем остаток времени от предыдущего захода и $time['goto']
$gt_last_ = @summ.time($args[1], $time['ost.'+$args[0]])
!@ приводим частоту и $time['goto'] к одной единице измерения. Внимание! Это секунды!
!@ отсчёт времени должен вестись от предыдущего значения. Т.е. от времени, ещё не изменённого интерпретатором календаря
frequenсy_ = @em.tag.getNum(@conv.time(@em.tag.getCont($obj_body_, 'frqs'), 'secunde', time['last.years']), 'secunde')
gt_secs_['last'] = @em.tag.getNum(@conv.time($gt_last_, '', time['last.years']), 'secunde')
gt_secs_['now'] = @em.tag.getNum(@conv.time($args[1], '', time['last.years']), 'secunde')
!@ вычисляем, сколько раз повторился указанный период
pik_['count']	= gt_secs_['last']/frequenсy_
!@ вычисляем остаток
rest_	= gt_secs_['last'] mod frequenсy_

!@ изменяем здоровье
$pik_['time'] = @summ.time($time['last.all'], "[secunde:<<gt_secs_['now']-rest_)>>]")
hours_['dig'] = @em.tag.getNum($pik_['time'],'hour')
minutes_['dig'] = @em.tag.getNum($pik_['time'],'minute')
$hours_['on_clock'] = @time.output.addition(hours_['dig'])
$minutes_['on_clock'] = @time.output.addition(minutes_['dig'])

!@ выводим на экран
$texts_['f_name'] = 'hero.'+ $property_
$texts_['p_recovery'] = property['hero.<<$property_>>.up']*pik_['count']
$texts_['p_output'] = " <font size=-1>(<<$p_name_>>: [<<$hours_['on_clock']>>:<<$minutes_['on_clock']>>])</font>"
gosub $texts_['f_name'], $texts_['p_recovery'], '+', '', $texts_['p_output']

!@ сохраняем остаток до следующего раза
$time['ost.'+$args[0]] = "[secunde:<<rest_>>]"
--- hero.vosst.time ---------------------------------