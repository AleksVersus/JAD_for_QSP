QSP-Game
! тексты в окно основного описания реплики торговцев с голосами
$args[0] = $args[0]	&	!	ключевое слово
$args[1] = $args[1]	&	!	идентификатор торговца
$args[2] = $args[2]	&	!	дополнительные данные
$args[3] = $args[3]	&	!	дополнительные данные
$args[9] = $args[9]	&	!	дополнительные данные

# b.d.s
local $seller_, $res_, r
if $args[1]<>'':
	$seller_['body'] = @get.obj.id($args[1])
	$seller_['name'] = @get.word.padez(@em.tag.getCont($seller_['body'],'tname'),'И')
	$seller_['sex'] = @em.tag.getCont($seller_['body'],'sex')
	$seller_['color'] = @em.tag.getNum($seller_['body'],'color')
	$seller_['fs_set'] = @em.tag.getCont($seller_['body'],'np')	& !@ features sets
end
if $seller_['color'] = '': $seller_['color'] = '006600'	&	!	не забыть вытащить цвет в базу
if $seller_['name'] = '': $seller_['name'] = @b.d.t('place.neverland')

:noMoney
if $args[0] = 'noMoney':
	if instr($seller_['fs_set'],'[чурка]') <> 0:
		$res_ += 'Нээт. Дажи тэбэ в долг нэ отпушщу.'
	else:
		$res_ += 'Не-ет. Я не отдаю вещи бесплатно.'
	end
	jump 'end_keep'
end
:приветствие
if instr($args[0], 'приветствие')<>0:
	if instr($seller_['fs_set'], '[чурка]')<>0:
		r = rand(1,5)
		if r = 1: $res_ += @int.din.text('[th]Доброе утро|Добрый день|Добрый вечер|Доброй ночи[/th], (дарагой/дарагая)!')
		if r = 2: $res_ += @int.din.text('В любое время дня и ночи я рад тэбе, (радной/радная)!')
		if r = 3: $res_ += @int.din.text('[th]Этим утром|Сегоднэ дньом|В этот вэчер|Нинче ночьу[/th] всё только для тебя, (братанъ/сэстра-братъ)!')
		if r = 4: $res_ += @int.din.text('Вах! Это ж (мой лубимый клыэнт/мая лубимая клыэнтка)!')
		if r = 5: $res_ += @int.din.text('Дасвиданьа... Ах. В смысле - прыуэт!!!')
	else:
		r = rand(1,6)
		if r = 1: $res_ += 'Давай посмотрим, что у нас есть.'
		if r = 2: $res_ += 'Лучшие товары для лучшего клиента.'
		if r = 3: $res_ += 'Всегда к твоим услугам!'
		if r = 4: $res_ += 'У нас - товар, у вас - денежки.'
		if r = 5: $res_ += 'У меня есть всё, что тебе нужно!'
		if r = 6: $res_ += 'У меня есть всё, что тебе нужно!'
	end
	jump 'end_keep'
end

:wtf
if $args[0]='':
	r = rand(1,5)
	if r = 1: $res_ += 'Чего?'
	if r = 2: $res_ += 'Повтори-ка ещё разик...'
	if r = 3: $res_ += 'Никак не пойму, чего ты от меня хочешь!'
	if r = 4: $res_ += @int.din.text('Вот ты сейчас сказа(л//ла) вообще непонятно.')
	if r = 5: $res_ += 'Несёшь какой-то бред.'
end
:end_keep
$result = "<b><font color=#<<$seller_['color']>>><<$seller_['name']>>:</font></b> - <<$res_>>"
--- b.d.s ---------------------------------