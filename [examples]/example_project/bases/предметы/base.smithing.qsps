QSP-Game ! База сборки предметов
! Внимание!!! Крафтятся только предметы, внесённые в базу base.new.obj и их модификации. Если вы добавили крафт предмета в эту базу, убедитесь, 
! что собираемый предмет присутствует в базе base.new.obj. Если предмет не внесён в базу, будет выведена ошибка.
$args[0] = $args[0]	&	!	ID локации кузницы
# base.smithing
local $res_
!@ подготавливаем временную базу с предметами
local $smitning_id_, smitning_kolvo_, $smitning_object_
if @get.daughter.obj($args[0],'','$smitning_id_') = 'true':
	loop local i, pit_ = 0, -1 while i < arrsize('$smitning_id_') step i += 1:
		pit_ = arrpos('$id_array', $smitning_id_[i])	& !@ pos_in_table
		$smitning_object_[i] = $object_array[pit_]
		smitning_kolvo_[i] = kolvo_array[pit_]
		$log_error += '<br>'+$smitning_id_[i]+' &gt;&gt; '+$smitning_object_[i]+' &gt;&gt; '+$str(smitning_kolvo_[i])
	end
end
!@ теперь так.
!@ титановый лук изготавливается из слитков титана. Любых слитков. Т.е. ему важно, чтобы предмет имел короткое слово "стержень_титановый" и вес "1".
!@ Т.о. нужно найти позиции всех элементов, содержащих строку  стержень_титановый, и набросать количество в сумме.

local $obj_
!@ ---------- все предварительные вычисления наличия компонентов --------------------------------------------------
local $regexp_ = '(\[:стержень_титановый:\][\s\S]*\[weight:100\]|\[weight:100\][\s\S]*\[:стержень_титановый:\])+'
$obj_['стержень_титановый_вес1'] = @search.similar.obj($regexp_)
obj_['стержень_титановый_вес1.штук'] = @em.tag.getNum($obj_['стержень_титановый_вес1'], 'колво')
!@ ----------- конец всех предварительных вычислений наличия компонентов -------------------------------------------

local $list_
!@ --------------------- непосредственно база порядка сборки ----------------------------------------------------
:титановый_лук
if $obj_['стержень_титановый_вес1'] <> 'false' and no obj_['стержень_титановый_вес1.штук']<40:
	! если условия удовлетоврительные, генерируем ссылку сборки предмета
	$list_ = '[1:'+$replace($obj_['стержень_титановый_вес1'],"[колво:<<obj_['стержень_титановый_вес1.штук']>>]",'[колво:40]')+':1]'
	$res_ = @gen.ahref.inMount($args[0],'[:титановый_лук:]',$list_,'титановый стержень (40 шт.)')
else
	$res_ = @gen.ahref.inMount($args[0],'[:титановый_лук:]','','титановый стержень (40 шт.)')
end
!@ --------------------- непосредственно база порядка сборки ----------------------------------------------------

$result = $res_
--- base.smithing ---------------------------------